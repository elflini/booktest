# INPUT문에서 관찰값 읽기 {#chapter3}


## CARDS문과 CARDS4문  

    

```{}
<SAS PROGRAM - CARDS>

OPTIONS LS=72 nodate nonumber;
DATA club1;
INPUT idno name $ team $ strtwght endwght;
CARDS;
1023 David red 189 165
1049 Amelia yellow 145 124
1219 Alan red 210 192
1246 Ravi yellow 194 177
1078 Ashley red 127 118
1221 jim yellow 220 .
;
PROC PRINT DATA=club1; RUN;
```  

      <OUTPUT>

                         Weight Club Members                          
                                             
         Obs    idno    name      team      strtwght    endwght

          1     1023    David     red          189        165
          2     1049    Amelia    yellow       145        124
          3     1219    Alan      red          210        192
          4     1246    Ravi      yellow       194        177
          5     1078    Ashley    red          127        118
          6     1221    jim       yellow       220          .

 - 출력된 결과는 VAR를 지정하지 않았으므로 INPUT문의 순서대로 출력됨.  
 
      

```{}
<SAS PROGRAM - CARDS4>

DATA club1;
INPUT idno name $ team $ strtwght endwght;
CARDS4;
1023 David red 189 165
1049 Amelia; yellow 145 124
1219 Alan red 210 192
1246 Ravi yellow; 194 177
1078 Ashley; red 127 118
1221 jim yellow 220 .
;;;;
PROC PRINT DATA=club1; RUN;
```  

      <OUTPUT>

                     Fitness Center Weight Club

      OBS    idno    name       team       strtwght    endwght

      1     1023    David      red           189        165
      2     1049    Amelia;    yellow        145        124
      3     1219    Alan       red           210        192
      4     1246    Ravi       yellow;       194        177
      5     1078    Ashley;    red           127        118
      6     1221    jim        yellow        220          .

 - 출력된 결과는 VAR를 지정하지 않았으므로 INPUT문의 순서대로 출력됨.  
 

※ 관찰값 읽기 2 - CARDS문과 CARDS4문

 • CARDS, CARDS4문은 자료의 시작을 알림.  
 • CARDS문은 관찰값에 (;)이 들어있지 않은 자료에 사용, 마지막은 RUN; 또는 ; 사용.   
 • CARDS4는 관찰값중 ;이 들어있는 자료가 혼합되어 있는 경우에 사용, 마지막은 ;;;;  
 
|CARDS; <br> <입력 DATA LINE> <br> ;|CARDS4; <br> < ; 을 가진 입력 DATA LINE> <br> ;;;;|
|:----------|:---|
|||  

<br>

## 결측값이 있는 경우  

    

```{}
<SAS PROGRAM>

DATA club1;
INPUT indo 1-4 name $ 6-11 team $ 13-18 strtwght 20-22 endwght 24-26;
CARDS;
1023 David  red    189 165
1049 Amelia yellow 145
1219 Alan   red    210 192
1246 Ravi   yellow     177
1078 Ashley red    127 118
1221 jim    yellow 220 .
RUN;
PROC PRINT DATA=club1;
RUN;
```

▶ Input 문에서 칼럼을 지정한 경우 변수 다음에 칼럼번호를 기입한다.  

▶ 문자 변수일 경우 $ 다음에 칼럼번호를 기입한다.  

▶ 주의할 점 : Input 문에서 칼럼번호를 지정한 경우 CARDS문 다음에 오는 관찰값들이 해당 칼럼안에 있어야한다. 전 프로그램과 다른 점은 Input문에서 칼럼을 지정하였으므로 CARDS문 다음에 오는 자료들이 칼럼에 맞추어서 입력이 되어야 한다.  

▶ 편리한 점 : 결측값을 빈 공백으로 놓고 (.)을 표시하지 않아도 된다. 물론 (.)으로 표시해도 상관없다.(칼럼을 지정하지 않을 경우 반드시 결측은 (.)으로 표시해야 한다.)  

    <OUTPUT>

                          Weight Club Members                          

         Obs    indo    name      team      strtwght    endwght

          1     1023    David     red          189        165
          2     1049    Amelia    yellow       145          .
          3     1219    Alan      red          210        192
          4     1246    Ravi      yellow         .        177
          5     1078    Ashley    red          127        118
          6     1221    jim       yellow       220          .

▶ 프로그램 상에서는 strtwght변수의 4번째 관찰값과 endwght변수의 2, 6번째 관찰값들이 (.)이 아닌 빈 공백이었음에도 결과상 (.)으로 처리한다.  

<br>

## 관찰값내에 공백이 있는 경우  


```{}
<SAS PROGRAM>

DATA club2;
INPUT idno 1-4 name $ 6-23 team $ 25-30 strtwght 32-34 endwght 36-38;
CARDS;
1023 David Shaw         red    189 165
1049 Amelia Serrano     yellow 145 124
1219 Alan Nance         red    210 192
1246 Ravi Sinha         yellow 194 177
1078 Ashley McKnight    red    127 118
1221 jim Brown          yellow 220 
RUN;
PROC PRINT DATA=club2;
RUN;
```
 
▶ 1개의 관찰값내에 공백이 포함되어 있는 경우 INPUT문에서 칼럼을 지정하지 않고 관찰값과 관찰값 사이는 공백으로 구분한다면, 관찰값을 구분하는 공백으로 인식하여 실제 관찰값의 수보다 많게 되므로 오류가 발생한다. 물론 칼럼 지정 시 결측값의 경우는 공백으로 처리한다.  

    <OUTPUT>

                                     Weight Club Members                   

               Obs    idno    name               team      strtwght    endwght

                1     1023    David Shaw         red          189        165
                2     1049    Amelia Serrano     yellow       145        124
                3     1219    Alan Nance         red          210        192
                4     1246    Ravi Sinha         yellow       194        177
                5     1078    Ashley McKnight    red          127        118
                6     1221    jim Brown          yellow       220          .

▶ 출력결과를 살펴보면 name변수의 각 관찰값들이 공백을 가지고 있고, INPUT문에서 관찰값의 시작 칼럼과 끝 칼럼을 지정하여 주었으므로 이를 하나의 관찰값으로 인식한다.  


※ 관찰값 읽기 3 - 관찰값내에 공백이 있는 경우 (INPUT문)  

 • 변수명 다음에 관찰값의 시작 칼럼과 끝 칼럼을 지정  
 
<br>

## INPUT문에서 변수 순서 바꾸기    




```{}
<SAS PROGRAM>

DATA club2;
INPUT team $ 25 name $ 6-23 strtwght 32-34 endwght 36-38;
CARDS;
1023 David Shaw         red    189 165
1049 Amelia Serrano     yellow 145 124
1219 Alan Nance         red    210 192
1246 Ravi Sinha         yellow 194 177
1078 Ashley McKnight    red    127 118
1221 jim Brown          yellow 220 
RUN;
PROC PRINT DATA=club2;
RUN;
```
  
▶ 칼럼을 지정한 경우  
   • INPUT문에서 나열된 변수 순으로 지정 칼럼에 가서 읽으므로 관찰값과 변수의 위치가 달라도 칼럼만 제대로 지정한다면 원래 하는 대로 관찰값을 변수에 매치시킨다.   
   • CARDS문 다음에 오는 관찰값들의 변수 수보다 같거나 더 적게 INPUT문에서 지정할 수도 있다.  

▶ CARDS문 다음에 오는 자료의 순서와 INPUT문에서의 변수 나열 순서와 다른 것을 알 수 있다. 처음에 team이라는 문자변수를 25번째 칼럼에서 한글자만 읽으므로 team변수에는 한 문자만 입력한다. name변수는 6-23칼럼, strtwght변수는 32-34칼럼, endwght변수는 36-38칼럼을 읽어 들인다.  

    <OUTPUT>
    
                                     Weight Club Members                

                    Obs    team    name               strtwght    endwght

                     1      r      David Shaw            189        165
                     2      y      Amelia Serrano        145        124
                     3      r      Alan Nance            210        192
                     4      y      Ravi Sinha            194        177
                     5      r      Ashley McKnight       127        118
                     6      y      jim Brown             220          .

  
▶ 출력결과는 PROC PRINT에서 VAR문을 지정하지 않았으므로 club2 DATASET에 기억된 순서에 의해 인쇄된다. 변수의 순서는 INPUT문에서 기재된 순서대로 기억하고 있음을 볼 수 있다. 또한 team변수에서는 25번째 칼럼만 지정하였으므로 읽어 들인 문자가 하나씩임을 볼 수 있다.  


※ 관찰값 읽기 4 - INPUT문에서 변수 순서 바꾸기  

 • INPUT문에서 변수의 칼럼 지정 : 변수 수보다 같거나 더 적게 INPUT문에서 읽음 

 • 변수 순서와 상관없이 INPUT문에서 나열된 순서로 읽음  

 • 2칼럼 이상을 차지하는 값 중에서도 특정 칼럼만 관찰값으로 읽을 수 있음   
 
<br>

## COMMA(,)가 포함된 관찰값 읽기  


```{}
<SAS PROGRAM>

DATA jansales;
INPUT item $ 1-16 amount COMMA5.;
CARDS;
trucks          1,382
jeeps           1,235
landrovers      2,391
;
PROC PRINT DATA=jansales;
	TITLE 'january Sales in Thousands';
	FORMAT amount DOLLAR9.;
RUN;
```
  
▶ 관찰값에 (,)가 포함되어 있는 경우에 관찰값을 읽는 방식과 PROC PRINT문에서 특정 변수의 관찰값에 특정 문자를 붙이는 것을 표현한다.  

▶ jansales DATASET의 input문에서 item $ 1-16 : 문자형 변수 1-16칼럼까지 읽는 것을 나타내며, amount comma5. : (,)를 포함하여 5자리로 읽는 것을 나타낸다. 단 comma4. 으로 읽으면 amount 변수의 첫 번째 값은 138로 인식하게 된다.  

▶ PROC PRINT에서 FORMAT 변수 DOLLAR9. : 변수의 관찰값들의 자릿수가 9자리로 지정되고 관찰값 앞에 $가 붙는다.  

▶ 주의할 점   
   • 해당 변수의 관찰값의 자리수와 FORMAT문 다음에 오는 자리수가 동일하거나 더 작은 경우에는 $표시가 붙지 않음.   
   • 관찰값도 지수표현법으로 바뀌어 표시되므로 충분하게 자리수를 지정하여 사용해야한다.  
   
    <OUTPUT>

                                 january Sales in Thousands                            
                                                             

                               Obs    item             amount

                                1     trucks           $1,382
                                2     jeeps            $1,235
                                3     landrovers       $2,391  
<br>

    FORMAT amount DOLLAR4로 한 경우

                                 january Sales in Thousands                             

                                 Obs    item          amount

                                  1     trucks         1382
                                  2     jeeps          1235
                                  3     landrovers     2391


※ 관찰값 읽기 5 - COMMA가 포함된 관찰값 읽기(INPUT)  

 • 컴마가 포함된 자료를 컴마를 포함하여 n 자리만큼 읽는다.   

```{}
  INPUT 변수명 COMMA자릿수.;
  INPUT cost COMMAn. ;   -cost변수는 ,를 포함하여 n자리로 읽음
```

※ PROC PRINT 형식2  

```{}
  PROC PRINT DATA = dataname ;
    VAR 변수명 ; 
    FORMAT 변수명 DOLLARn. ; 
  RUN;
```

 • 해당변수의 자릿수를 n 자리로 출력시키며 관찰값 앞에 $표시를 붙인다.  
 
<br>

## COLON(:)을 이용하여 관찰값 읽기  

▶ 관찰값을 읽은 후 문자열의 길이를 지정한다.  


```{}
<SAS PROGRAM>

DATA jansales;
INPUT item : $10. amount : COMMA5.;
CARDS;
trucks 1,382
jeeps 1,235
landrovers 2,391
;
PROC PRINT DATA=jansales;
TITLE 'january Sales in Thousands';
FORMAT amount DOLLAR9.;
RUN;
```
  
▶ INPUT문에서 나열된 변수 순서대로 관찰값을 읽을 때 콜론은 읽은 관찰값에 대하여 그 다음에 오는 일정 형식을 지정할 수 있다.  
  ※ NOTE :   
   • item변수와 amount변수를 읽어오는데 먼저 item문자변수의 **관찰값을 읽어온 후 문자열의 길이를 10자리로 지정**, amount변수는 (,)를 포함하여 5자리로 읽는다.  
   • **관찰값들의 배열이 일정 칼럼으로 지정되어 있지 않아도 된다**는 것이 특징  
   • item변수의 관찰값들 중에서 지정된 자리 10자가 초과할 경우에는 일단 공백까지 읽은 후에 왼쪽에서부터 10자리만을 관찰값으로 취하게 된다.  
   • COLON을 사용하여 읽을 경우 관찰값내에 공백이 있을 경우 사용할 수 없다.  
   
    <OUTPUT>
    
                                 january Sales in Thousands                           

                               Obs    item             amount

                                1     trucks           $1,382
                                2     jeeps            $1,235
                                3     landrovers       $2,391

▶ INPUT문에서 item : $9. 으로 주어졌다면 item변수는 9자리로 인식되고 세 번째 관찰값은 마지막 1자리가 부족하므로 landrover로 출력한다.  

▶ amount변수는 (,)를 포함하여 5자리이고, 관찰값 앞에 $표시를 붙이는데 9자리로 지정하였으므로 제대로 출력한다.  

※ 관찰값 읽기 6 - COLON(:)을 이용하여 관찰값 읽기(INPUT)  

```{}
  INPUT 변수명 : [ $n. ];
```

 • 관찰값을 읽은 후 n 자리만큼 길이 지정

## AMPERSAND(&)을 이용하여 관찰값 읽기  


```{}
<SAS PROGRAM>

DATA club2;
INPUT idno name & $18. team $ strtwght endwght;
CARDS;
1023 David Shaw  red 189 165
1049 Amelia Serrano  yellow 145 124
1219 Alan Nance  red 210 192
1246 Ravi Sinha  yellow 194 177
1078 Ashley McKnight  red 127 118
1221 jim Brown  yellow 220 .
RUN;
PROC PRINT DATA=club2;
RUN;
```
  
▶ CARDS문 다음에 오는 관찰값들의 칼럼이 일정하지 않은 경우에 &(Ampersand)를 이용하여 관찰값을 읽는 방법이다.  

▶ 공백이 1개 이상이면 관찰값과 관찰값을 분리하여 길이를 지정하는 경우에 (:)를 사용한다. (관찰값내 공백이 삽입되어 있는 경우에는 사용 불가)  
▶ 관찰값내 공백이 있으면서 칼럼으로 일정하게 정렬이 되지 않아 시작과 끝 칼럼을 지정할 수 없는 경우에 (&)를 사용한다. 즉 (&)는 2개 이상의 공백 전까지 하나의 값으로 인식한다.  

  ※ NOTE :   
   • name변수 뒤에 (&)를 사용하므로 2개 이상의 공백 다음 값 전까지 읽으므로 관찰값이 David Shaw까지 들어가며 name변수는 문자 변수로 18자리로 인식한다.  
   • name변수 뒤에 (&)가 있으므로 칼럼 정렬하지 않아도 되나 바로 뒤에 오는 team변수의 값이 하나의 공백으로 구분된다면 team변수의 값을 name변수의 값으로 계속해서 읽으므로 오류가 발생한다.  
   
    <OUTPUT>
    
                                     Weight Club Members                   

               Obs    idno    name               team      strtwght    endwght

                1     1023    David Shaw         red          189        165
                2     1049    Amelia Serrano     yellow       145        124
                3     1219    Alan Nance         red          210        192
                4     1246    Ravi Sinha         yellow       194        177
                5     1078    Ashley McKnight    red          127        118
                6     1221    jim Brown          yellow       220          .


  ※ NOTE :   
   • 칼럼을 지정하지 않아도 공백을 포함하고 있는 name변수의 관찰값들이 원하는 대로 출력된다.  
   • 결측값은 CARDS문 다음에 오는 자료에서 (.)으로 표시해 주어야 한다.  
   
<br>

## INPUT문에서 @사용과 IF문  


```{}
<SAS PROGRAM>

DATA redteam;
	INPUT team $ 13-18 @ ;
	IF team='red';
	INPUT idno 1-4 strtwght 20-23 endwght 24-26;
	CARDS;
1023 David  red    189 165
1049 Amelia yellow 145
1219 Alan   red    210 192
1246 Ravi   yellow     177
1078 Ashley red    127 118
1221 jim    yellow 220 .
;
RUN;
PROC PRINT DATA=redteam;
	TITLE 'Red Team';
RUN;
```

▶ 특정 변수의 특정 관찰값만을 선택하는 방법으로 INPUT문에서 @를 사용하는 것과 IF문을 사용하는 방법이다.  

▶ @: 다른 INPUT문이 나올 때까지 기다린다. 13-18번째 칼럼에 있는 값을 문자변수 team으로 읽고 다음 INPUT문이 나올 때까지 기다린다. 그 다음 IF문에 의해서 team변수의 값이 red인 것만 취하게 된다. 그리고 INPUT문이 나오면 그 다음에 지정된 변수의 칼럼에 맞추어 관찰값을 읽는다.  

▶ IF: "IF 조건식;”의 형식으로 조건에 맞으면 참으로 인식한다. team변수의 값이 ‘yellow’인 것들은 거짓으로 인식하게 되므로 제외하고 'red'인 것만 취하여 생성된 DATASET인 red team을 출력한다.  

    <OUTPUT>

                      Red Team                        

             Obs    team    idno    strtwght    endwght

              1     red     1023       189        165
              2     red     1219       210        192
              3     red     1078       127        118

▶ 출력결과 ‘red'인 것만 출력됨을 알 수 있다.  


※ IF 조건식   

 • 입력되는 관찰값 중 조건식에 맞는 관찰값만 선택하여 새로운 DATASET을 만든다.  

IF 조건식 ; (= IF 반대조건식 THEN DELETE ; )  
  예)  
```{}
IF team = 'red'; (= IF team ^='red' THEN DELETE ; )
```

<br>

## INPUT문에서 자릿수 지정하기  

  - 읽어 들일 관찰값의 자릿수는 변수명 뒤에 지정한다.  
  - 공백 있는 문자 변수를 읽을 때 유용하다.  


```{}
<SAS PROGRAM>

DATA club1;
	INPUT idno name $18. team $ 25-30 strtwght endwght;
	*INPUT idno name $18. team $ strtwght endwght;
	*INPUT idno 1-4 name $6-23 team $ 25-30 strtwght endwght;
	CARDS;
1023 David Shaw         red    189 165
1049 Amelia Serrano     yellow 145 124
1219 Alan Nance         red    210 192
1246 Ravi Sinha         yellow 194 177
1078 Ashley McKnight    red    127 118
1221 jim Brown          yellow 220 .
RUN;
PROC PRINT DATA=club1;
	TITLE 'Weight Club Members';
RUN;
```
  
▶ 공백을 포함하는 문자변수를 읽어 들일 때  
   1) 시작 칼럼과 끝 칼럼을 지정한다.  
    예) name $ 6-23  
   2) 문자변수의 해당 자릿수 지정한다. 즉, (끝칼럼-시작칼럼)+1  
    예) name $ 18. (=23-6+1)  

```{}
   INPUT idno name $ 18. team $ 25-30 strtwght endwght;
   = INPUT idno name $ 18. team $ strtwght endwght;
   = INPUT idno 1-4 name $ 6-23 team $ 25-30 strtwght endwght;
```

      <OUTPUT>

                            Weight Club Members                            

         Obs    idno    name               team      strtwght    endwght

          1     1023    David Shaw         red          189        165
          2     1049    Amelia Serrano     yellow       145        124
          3     1219    Alan Nance         red          210        192
          4     1246    Ravi Sinha         yellow       194        177
          5     1078    Ashley McKnight    red          127        118
          6     1221    jim Brown          yellow       220          .


▶ 출력결과는 위에서 제시된 3개의 INPUT문 모두 동일한 것을 알 수 있다.  


※ 관찰값 읽기 7 - INPUT문에서 자릿수 지정하기  

INPUT 변수명 [ $ 자릿수. ];  
  예)
```{}
INPUT test $5. test1 4. ;     - 문자변수 test는 5자리, 숫자변수 test1은 4자리
```

<br>

## +를 이용한 포인터 이동  

  - (+)뒤에 오는 자릿수만큼 포인터를 오른쪽으로 이동한다.  


```{}
<SAS PROGRAM>

DATA points;
	INPUT team $ 1-6 points 12-13;
	*INPUT team $6. +5 points 2.;
	*INPUT team $ points;
	CARDS;
red        59
blue       95
yellow     63
green      76
;
PROC PRINT;
RUN;
```
  
▶ <span style="color:blue">INPUT</span> team $ 1-6 points 12-13; 변수명 뒤에 해당 칼럼을 지정하여 사용한다. 즉, INPUT문에서 첫 번째에 해당하는 것과 같이 team이라는 문자변수(변수명 뒤에 $) 뒤에 칼럼의 시작과 끝을 지정한다.  
  
▶ <span style="color:blue">INPUT</span> team $ 6. +5 points 2; team 변수는 column1부터 column6 까지 읽고 포인터를 오른쪽으로 5자리 이동 후 두 자리를 points 변수로 읽는다.  

▶ <span style="color:blue">INPUT</span> team $ points ; 관찰값과 관찰값 사이가 공백으로 구별되는 경우에 INPUT문 뒤에 변수명을 나열한다.  

    <OUTPUT>

               Weight Club Members              

            Obs    team      points

              1     red         59
              2     blue        95
              3     yellow      63
              4     green       76


▶ PROC PRINT의 결과에 의한 출력은 앞에서 어느 INPUT문을 사용하든지 같은 DATASET을 만들기 때문에 출력된 결과는 동일하다.  


※ 관찰값 읽기 8 - (+)를 이용한 포인터의 이동   

 • “+자릿수”를 지정하면 현 위치에서 해당 자릿수만큼 포인터의 위치를 오른쪽으로 이동  

 • 특정 칼럼을 제외시킬 때에 주로 이용  

INPUT 변수명 [ $ 자릿수. ] [[ +자릿수 ] 변수명 [ $ 자릿수. ]];  
  예)  
```{}
INPUT test $ 10. +2 test1 ; 문자변수 test의 관찰값을 10자리로 읽고 2자리 건너 띄어서 (오른쪽 이동하여) test1변수의 관찰값을 읽음
```

<br>

## INPUT문에서 @@ 사용하기  


```{}
<SAS PROGRAM>

DATA bodyfat;
	INPUT sex $ fatpct @@;
	*INPUT sex $ fatpct @;
	*INPUT sex $ fatpct ;
	CARDS;
m 13.3 f 22
m 22   f 23.2
m 16   m 12
;
PROC PRINT DATA=bodyfat;
	TITLE'Results of Bodyfat Testing';RUN;
```

▶ @@표시는 @와 같이 포인터 제어 기능을 한다.  
▶ @는 Input문에 의해 제어되나 @@는 관찰값에 의해 제어된다. 한 줄에 존재하는 관찰값을 모두 읽을 때까지 포인터의 이동을 정지시킨다.  
▶ INPUT문만 다음과 같이 바꾸면 출력된 결과는 달라지게 된다.  

```{}
INPUT sex $ fatpct ; or INPUT sex $ fatpct @;
```

    <OUTPUT>

      출력1.
                     Results of Bodyfat Testing            

                       Obs    sex    fatpct
                        1      m      13.3
                        2      f      22.0
                        3      m      22.0
                        4      f      23.2
                        5      m      16.0
                        6      m      12.0

<br>

      출력2.  
                    Results of Bodyfat Testing            

                      Obs    sex    fatpct
                        1      m      13.3
                        2      m      22.0
                        3      m      16.0

  
▶ 출력2에서 출력1의 관찰값 중 1, 3, 5번째 관찰값만이 인식된 것을 볼 수 있다.   


※ 관찰값 읽기 9 - INPUT문에서 @@ 사용하기  

 • 줄 포인터의 이동을 제어  

 • @@는 한 줄에 존재하는 관찰값들을 전부 읽을 때까지 줄 포인터의 이동을 보류  

 • 변수의 수가 적은 경우 INPUT문에서 제시되는 변수의 수보다 자료를 많이 나열할 때 유용  

```{}
INPUT 변수명 [[$ ] 변수명 [$ ] ] @@ ;
```

<br>

## 여러 개의 INPUT문 사용하기  


```{}
<SAS PROGRAM>

DATA club2;
	INPUT idno 1-4;
	INPUT ;
	INPUT strtwght 1-3 endwght 5-7 ;
	CARDS ;
1023 David Shaw
red
189 165
1049 Amelia Serrano
yellow
145 124
1219 Alan Nance
red
210 192
1246 Ravi Sinha
yellow
194 177
1078 Ashley McKnight
red
127 118
1221 jim Brown
yellow
220 .
;
RUN;
PROC PRINT DATA=club2;
	TITLE 'Beginning and Ending Weights';
RUN;
```

▶ 자료가 첫 번째 행에 idno, 두 번째 행에 team, 세 번째 행에 strtwght, endwght의 변수로 구성되어 있는 경우  
  1) 첫 번째 INPUT : idno변수의 관찰값을 1행의 1-4번째 칼럼에서 읽는다.  
  2) 두 번째 INPUT : 다음 행으로 넘어간다.  
  3) 세 번째 INPUT : strtwght변수를 1-3번째 칼럼, endwght변수를 5-7번째 칼럼에서 읽는다. 그 후 위와 동일한 방법으로 모든 자료를 읽게 된다.  
▶ INPUT문의 끝인 (;)을 지나면서 줄 포인터가 다음 행으로 이동되는 성질을 이용한 방법이다.  

    <OUTPUT>

                   Beginning and Ending Weights          

                Obs    idno    strtwght    endwght

                  1     1023       189        165
                  2     1049       145        124
                  3     1219       210        192
                  4     1246       194        177
                  5     1078       127        118
                  6     1221       220          .


▶ INPUT문에서 idno, strtwght, endwght 변수만 읽었으므로 3개의 변수 6개의 OBS (Observation)만 인식한다.  


※ 관찰값 읽기 10 - 하나의 OBS가 여러 행에 입력되어 있는 자료 읽기(여러 개의 INPUT문 사용하기)  

 • 하나의 OBS(또는 레코드)가 여러 줄에 입력되어 있는 자료를 하나의 DATASET으로 만들기 위해 여러 개의 INPUT문을 사용하여 변수에 대응  
 • 하나의 DATASET을 형성할 때 하나의 OBS에 해당하는 행만큼 INPUT문 사용가능  
 
<br>

## INPUT문에서 SLASH(/) 사용하기  


```{}
<SAS PROGRAM>

DATA club2;
	INPUT idno 1-4; / / strtwght 1-3 endwght 5-7 ;
	CARDS ;
1023 David Shaw
red
189 165
1049 Amelia Serrano
yellow
145 124
1219 Alan Nance
red
210 192
1246 Ravi Sinha
yellow
194 177
1078 Ashley McKnight
red
127 118
1221 jim Brown
yellow
220 .
;
RUN;
PROC PRINT DATA=club2;
	TITLE 'Beginning and Ending Weights';
RUN;
```

▶ /(Slash) : 줄 포인터를 현재 행에서 다음 행의 처음으로 옮기는 기능  

▶ INPUT문  
   idno변수를 1행의 1-4번째 칼럼을 읽고 (/)가 나오므로 2번째 행으로 또 (/)가 나오므로 3번째 행으로 포인터가 이동되고, 3번째 행에서 strtwght변수를 1-3번째 칼럼, endwght변수를 5-7번째 칼럼으로 인식 되며 (;)을 만나면 4번째 행으로 포인터를 이동한다.  

    <OUTPUT>

            Beginning and Ending Weights          

          Obs    idno    strtwght    endwght

            1     1023       189        165
            2     1049       145        124
            3     1219       210        192
            4     1246       194        177
            5     1078       127        118
            6     1221       220          .

  
▶ 출력된 결과는 앞에서와 같게 나타난다.  

※ 관찰값 읽기 11 - 하나의 OBS가 여러 행에 입력되어 있는 자료 읽기(INPUT문에서 / 사용하기)  

 • 하나의 OBS(또는 레코드)이 여러 줄에 입력되어 있는 자료를 하나의 DATASET으로 만들기 위해 /(slash)를 사용가능  
 • 현재 행에서 바로 다음 행의 처음으로 옮기는 기능  

  INPUT test1 /// test2 ;     
 • 첫 번째 행에서 test1변수의 관찰값 읽고 줄 포인터를 4번째 행으로 이동하여 test2 변수의 관찰값을 읽음  
 
<br>

## 줄 포인터 # 사용하기  


```{}
<SAS PROGRAM>

DATA club2;
	INPUT #2 team $ 1-6 #1 name $ 6-23 idno 1-4 
             #3 strtwght 1-3 endwght 5-7 ;
	CARDS ;
1023 David Shaw
red
189 165
1049 Amelia Serrano
yellow
145 124
1219 Alan Nance
red
210 192
1246 Ravi Sinha
yellow
194 177
1078 Ashley McKnight
red
127 118
1221 jim Brown
yellow
220 .
;
RUN;
PROC PRINT DATA=club2;
	TITLE 'Fitness Center Weight Club';
RUN;
```

▶ #: 입력되는 현재 행의 위치에서 #다음에 오는 숫자의 행으로 이동시키는 역할을 한다.  
▶ INPUT문  
   #2: 2행으로 가서 1-6번째 칼럼을 문자변수 team 읽기.  
   #1: 1행으로 가서 문자변수 name을 6-23번째 칼럼, idno변수는 1-4번째 칼럼을 읽기.  
   #3: 3행으로 가서 1-3번째 칼럼은 strtwght변수, 5-7번째 칼럼은 endwght변수 넣기.  
   이와 같은 방법이 자료 끝인 (;)전까지 반복  

    <OUTPUT>

                       Fitness Center Weight Club                     

      Obs    team      name               idno    strtwght    endwght

        1     red       David Shaw         1023       189        165
        2     yellow    Amelia Serrano     1049       145        124
        3     red       Alan Nance         1219       210        192
        4     yellow    Ravi Sinha         1246       194        177
        5     red       Ashley McKnight    1078       127        118
        6     yellow    jim Brown          1221       220          .

  
▶ PRINT에서 변수지정을 하지 않았으므로 club2 DATASET에 있는 변수들을 출력하는데 INPUT문에서 지정된 순서대로 출력되는 것을 알 수 있다.  

※ 관찰값 읽기 12 - 하나의 OBS가 여러 행에 입력되어 있는 자료 읽기(줄 포인터 # 사용하기)  

 • 하나의 OBS(또는 레코드)이 여러 줄에 입력되어 있는 자료를 입력하는 방법으로 사용  
 • #n은 # 다음에 오는 숫자(n)에 해당하는 행으로 이동하는 역할  
 
 #숫자: 숫자에 해당하는 줄로 이동  
INPUT test1 #2 test2: 첫 번째 행에서 test1변수의 관찰값을 읽고 줄 포인터를 2번째 행으로 이동하여 test2 변수의 관찰값을 읽음  

