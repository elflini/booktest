# SET문 활용 {#chapter4}



## 칼럼 포인터 @ 사용하기와 LABEL문 사용하기  

     

```{}
<SAS PRoGRAM> 

DATA nasa;
	INPUT YEAR 4. @7 TOTAL comma6.
		@15 PTOTAL comma6. @20 PLIGHT comma6.
		@28 PSCIENCE comma6. @37 PAIRTRAN comma6.
		@43 FTOTAL comma6. @49 FFLIGHT comma6.
		@58 FSCIENCE comma6. @64 FAIRTRAN comma6.;
	LABEL     total='Total Outlays'
	         ptotal='Performance: Total'
			pflight='Performance: Space Flight'
		   pscience='Performance: Space Science Applications'
		   pairtran='Performance: Air Transport and Other'
		     ftotal='Facilities: Total'
			fflight='Facilities: Space Flight'
		   fscience='Facilities: Space Science Applications'
		   fairtran='Facilities: Air Transport and Other';
CARDS;
1966  5,933   5,361 3,819  1,120    422      572   391   63    118
1967  5,426   5,137 3,477  1,160    500      289   172   47     70
1968  4,726   4,599 3,028  1,061    510      127    69   29     29
1969  4,252   4,187 2,754    893    540       65    27   21     17
1970  3,753   3,699 2,195    963    541       54    14   21     19
1971  3,382   3,338 1,877    926    535       44     8    6     30
1972  3,423   3,373 1,727  1,111    535       50    13    7     30
1973  3,316   3,271 1,532  1,220    519       45     5   11     29
1974  3,256   3,181 1,448  1,156    577       75    25   12     38
1975  3,226   3,181	1,500  1,076    606       85    35    9     42
1976  3,670   3,549	1,934    969    646      121    66   11     43
1977  3,945   3,840	2,195  1,002    643      105    56    4     45
1978  3,984   3,860	1,104    964    692      124    56    8     60
1979  4,187   4,054	2,175  1,144    735      133    41    9     83
1980  4,850   4,710	2,556  1,341    813      140    38    5     97
1981  5,241   5,274	3,026  1,380    868      147    26    4    117
1982  6,035   5,926	3,526  1,454    946      109    17    3     89
1983  6,664   6,556	4,027  1,486    1,043    108    26    .     82
1984  7,048   6,856	4,037  1,667    1,152    192    44   20    128
1985  7,251   7,001	3,852  1,834    1,318    247    70   23    154
1986  7,404   7,105	3,787  2,101    1,317    299   107   26    166
;
```

▶ 칼럼 포인터 @ 사용  
   • @ 단독 사용 : 다른 INPUT문이 나오기를 기다리는 역할을 한다.  
   • @ 칼럼 번호 : 해당 칼럼으로 포인터를 옮겨 기다리는 역할을 한다.  
   • 결측값 : 칼럼 포인터를 지정하여 사용하는 경우 빈 칸으로 두어도 결측값으로 인식한다.  

▶ LABEL문  
   • 변수에 대한 설명을 붙이고자 할 때 사용한다. (**변수명=‘내용’**)  
   • 단, 변수 이름은 바뀌지 않고 부가 설명만 첨가된다.  
   

※ 관찰값 읽기 13 - 칼럼 포인터 @ 사용하기   

 • INPUT 문에서 시작 칼럼과 끝 칼럼을 지정하는 방법 대신 @칼럼번호의 형식으로 칼럼 포인터를 사용할 수 있다.  

 • @칼럼번호를 지정하면 @다음에 해당하는 숫자의 칼럼으로 포인터가 이동  
INPUT 변수명 @ 칼럼번호 변수명 ;   해당칼럼으로 포인터 이동  
INPUT test1 2. @5 test2 5.;
첫 번째 관찰값은 2자리로 test1변수에 입력되고 5번째 칼럼으로 포인터를 이동하여 5자리, 즉, 5-9번째 칼럼을 test2변수의 관찰값으로 읽음  

※ LABEL문 사용하기  

 • 변수에 대한 설명을 붙이고자 할 때 사용되며 변수명=‘내용’으로 사용  

 • PROCEDURE 다음에 변수명 대신 ‘내용’으로 출력  

 • 변수에 대한 상세한 설명을 붙일 수 있다.  

```{}
LABEL 변수명=‘내용’;
```

<br>

## PROC CONTENTS의 형식  

    
    
```{}
<SAS PROGRAM>

PROC CONTENTS DATA=nasa;
RUN;
```

▶ SAS DATASET의 제반 정보를 보는데 이용한다.  
   • 변수에 대한 정보, 관찰값의 수, 생성일시 등 여러 가지 사항을 확인가능하다.  
   • PROC CONTENTS 뒤에 SAS DATASET명을 써 주면 된다.  


※ PROC CONTESTS의 형식  

 • CONTESTS PROCEDURE는 SAS dataset의 정보 즉 변수와 자료들의 특성을 보는데 사용  

```{}
PROC CONTENTS DATA=dataset명 ; RUN;  
```

    <OUTPUT>

                               CONTENTS 프로시저

    데이터셋 이름  WORK.NASA                               관측치         21
    멤버 유형      DATA                                    변수           10
    엔진           V9                                      인덱스         0
    생성일         *************************************   관측치 길이    80
    마지막 수정일  *************************************   삭제된 관측치  0
    보호                                                   압축여부       NO
    데이터셋 유형                                          정렬           NO
    라벨
    데이터 표현    WINDOWS_32
    엔코딩         euc-kr  Korean (EUC)


                            엔진/호스트 관련 정보

    데이터셋 페이지 크기               8192
    데이터셋 페이지 번호               1
    첫 번째 데이터 페이지              1
    페이지 당 최대 관측치 수           101
    첫 번째 데이터 페이지의 관측치 수  21
    데이터셋 수리의 번호               0
    파일 이름                          D:\내문서\My SAS
                                       Files\_TD4864\na
    생성된 릴리즈                      9.0101M3
    생성된 호스트                      XP_PRO


                          변수와 속성 리스트(오름차순)

     #   변수       유형   길이   라벨

    10   FAIRTRAN   수치      8   Facilities: Air Transport and Other
     8   FFLIGHT    수치      8   Facilities: Space Flight
     9   FSCIENCE   수치      8   Facilities: Space Science Applications
     7   FTOTAL     수치      8   Facilities: Total
     6   PAIRTRAN   수치      8   Performance: Air Transport and Other
     4   PFLIGHT    수치      8   Performance: Space Flight
     5   PSCIENCE   수치      8   Performance: Space Science Applications
     3   PTOTAL     수치      8   Performance: Total
     2   TOTAL      수치      8   Total Outlays
     1   YEAR       수치      8

<br>

## 특정 변수를 취하여 새로운 DATASET 만들기 (KEEP문)  

    

```{}
<SAS PROGRAM>

DATA perform;
	SET nasa (keep=year ptotal pflight pscience pairtran);
RUN;
/* 아래와 같이해도 같은 작업을 수행한다 
DATA perform;
SET nasa;
keep year ptotal pflight pscience pairtran;
run;
*/
PROC PRINT DATA=perform;
	TITLE 'NASA Performance-Related Expenditures';
RUN;
```

▶ SET문과 특정 변수만 취하는 KEEP문을 이용하여 새로운 DATASET만들기  
   • SET문 :  
    ⅰ) 두 개 이상의 DATASET을 붙일 때  
    ⅱ) 이미 생성된 DATASET의 특정 부분만 취할 때  
   • KEEP문 :  
    ⅰ) SET문 안에서 KEEP문을 이용하여 원하는 특정변수만 취한다.  
    ⅱ) 버릴 변수의 수가 취하여야 할 변수의 수보다 많은 경우   


※ 특정 변수를 취하여 새로운 DATASET만들기 (SET, KEEP문)  

 • SET문을 이용하여 이미 작성된 DATASET을 토대로 새로운 이름의 DATASET 생성  

 • SET문에서 KEEP문을 사용하여 원하는 변수들만을 취할 수 있으며 전체 변수들 중 취해야 하는 변수의 수가 적은 경우에 유용  

```{}
DATA 새로운 DSTASET명;
SET 기 생성된 DATASET명 (KEEP = 취할 변수명 [변수명...]);
또는
DATA 새로운 DSTASET명;
SET 기 생성된 DATASET명
KEEP 취할 변수명 [변수명...]);
```

    <OUTPUT>

                 NASA Performance-Related Expenditures                 

        Obs    YEAR    PTOTAL    PFLIGHT    PSCIENCE    PAIRTRAN
          1    1966     5361       3819       1120         422
          2    1967     5137       3477       1160         500
          3    1968     4599       3028       1061         510
          4    1969     4187       2754        893         540
                                  (생략)
         19    1984     6856       4037       1667        1152
         20    1985     7001       3852       1834        1318
         21    1986     7105       3787       2101        1317

  
▶ 출력할 때 변수명 대신 해당 LABEL이 찍히도록 하려면 PROC PRINT DATA=perform LABEL; 과 같이 하면 된다.  


<br>

## 특정 변수를 제외하고 새로운 DATASET 만들기 (DROP문)  

    

```{}
<SAS PROGRAM>

DATA perform2;
	SET nasa (DROP=total ftotal fflight fscience fairtran);
RUN;
/* 아래와 같이해도 같은 작업을 수행한다
DATA perform2;
SET nasa;
drop total ftotal fflight fscience fairtran;
run;
*/
PROC PRINT DATA=perform2;
	TITLE 'NASA Performance-Related Expenditures';
RUN;
```

▶ SET문과 특정 변수를 제외하는 DROP문을 이용하여 새로운 DATASET만들기  
   • SET문 :  
    ⅰ) 두 개 이상의 DATASET을 붙일 때  
    ⅱ) 이미 생성된 DATASET의 특정 부분만 취할 때  
   • DROP문 :  
    ⅰ) SET문 안에서 DROP문을 이용하여 원하지 않는 변수를 제외한다.  
    ⅱ) 취할 변수의 수가 제외해야 할 변수의 수보다 많은 경우   


※ 특정 변수를 제외하고 새로운 DATASET만들기 (SET, DROP문)  

 • SET문을 이용하여 이미 작성된 DATASET을 토대로 새로운 이름의 DATASET 생성  

 • SET문에서 DROP문을 사용하여 원하는 변수들만 제외할 수 있으며 전체 변수들 중 취해야 하는 변수의 수가 많은 경우에 유용     

```{}
DATA 새로운 DSTASET명;
SET 기 생성된 DATASET명 (DROP = 취할 변수명 [변수명...]);
또는
DATA 새로운 DSTASET명;
SET 기 생성된 DATASET명
DROP 취할 변수명 [변수명...]);
```

    <OUTPUT>

                 NASA Performance-Related Expenditures

        OBS    YEAR    PTOTAL    PFLIGHT    PSCIENCE    PAIRTRAN
          1    1966     5361       3819       1120         422
          2    1967     5137       3477       1160         500
          3    1968     4599       3028       1061         510
          4    1969     4187       2754        893         540
          5    1970     3699       2195        963         541

                                  (생략)

         17    1982     5926       3526       1454         946
         18    1983     6556       4027       1486        1043
         19    1984     6856       4037       1667        1152
         20    1985     7001       3852       1834        1318
         21    1986     7105       3787       2101        1317

<br>

## 특정 관찰값부터 시작하여 DATASET 만들기(FIRSTOBS문)  

    

```{}
<SAS PROGRAM>

DATA NASA2;
	SET NASA(FIRSTOBS=12);
RUN;
```

▶ 전체 자료 중 원하는 위치부터 끝까지 자료를 지정하는 방법이다. 즉, NASA2 DATASET을 만드는데 NASA DATASET에서 12번째 관찰값부터 마지막 관찰값까지 지정하는 방법이다.   


※ 특정 관찰값부터 시작하여 DATASET만들기 (SET, FIRSTOBS문)  

 • SET문에서 FIRSTOBS= 다음에 숫자를 지정하면 첫 번째 줄이 아닌 지정된 숫자 번째 줄부터 관찰값으로 인식  

 • FIRSTOBS문은 읽고자 하는 관찰값 앞에 몇 개의 불필요한 줄이 존재할 경우에 유용하게 사용된다.  

```{}
DATA 새로운 DSTASET명;
SET 기 생성된 DATASET명 ( FIRSTOBS = 숫자 );
```

    <OUTPUT>

                                               NASA Expenditures
                                                   1977-1986

     OBS    YEAR    TOTAL    PTOTAL    PFLIGHT    PSCIENCE    PAIRTRAN    FTOTAL    FFLIGHT    FSCIENCE    FAIRTRAN

       1    1977     3945     3840       2195       1002         643        105        56          4           45
       2    1978     3984     3860       1104        964         692        124        56          8           60
       3    1979     4187     4054       2175       1144         735        133        41          9           83
       4    1980     4850     4710       2556       1341         813        140        38          5           97
       5    1981     5241     5274       3026       1380         868        147        26          4          117
       6    1982     6035     5926       3526       1454         946        109        17          3           89
       7    1983     6664     6556       4027       1486        1043        108        26          .           82
       8    1984     7048     6856       4037       1667        1152        192        44         20          128
       9    1985     7251     7001       3852       1834        1318        247        70         23          154
      10    1986     7404     7105       3787       2101        1317        299       107         26          166

<br>

## 기존 DATASET으로 다수의 새로운 DATASET 만들기  

    
 
```{}
<SAS PROGRAM>

DATA perform(keep=ptotal pflight pscience pairtran)
	 facil  (keep=ftotal fflight fscience fairtran);
	SET nasa;
RUN;
PROC PRINT DATA=perform;
RUN;
PROC PRINT DATA=facil;
RUN;
```

▶ 이미 생성된 DATASET으로부터 하나 이상의 DATASET을 새로이 만들고자 할 경우에 DATA문에 생성하고자 하는 DATASET명들을 나열하는 방법이다.  

▶ 하나의 DATASET을 가지고 특정 변수만 취하여 여러 개의 DATASET을 생성하는 것으로 DATA문과 KEEP(DROP)문 등을 이용하는 방법이다.  


※ 기존 DATASET을 이용하여 다수의 새로운 DATASET 만들기  

 • 기 생성된 DATASET으로부터 둘 이상의 DATASET을 새로이 만들고자 할 경우에 DATA문 다음에 생성하고자 하는 DATASET명들을 나열하면 된다.  

 • 새로운 DATASET에 특정 변수만을 입력하고자 할 경우에는 KEEP문을 이용하여 변수들을 지정하면 된다.  

```{}
DATA	새로운 DSTASET명(KEEP=변수명들)
	새로운 DSTASET명(KEEP=변수명들);
SET 기 생성된 DATASET명;
```

    <OUTPUT>

                        NASA Expenditures : Performance

                OBS    PTOTAL    PFLIGHT    PSCIENCE    PAIRTRAN
                  1     5361       3819       1120         422
                  2     5137       3477       1160         500
                  3     4599       3028       1061         510

                                     (생략)

                 18     6556       4027       1486        1043
                 19     6856       4037       1667        1152
                 20     7001       3852       1834        1318
                 21     7105       3787       2101        1317

<br>

                         NASA Expenditures : Facilities

                OBS    FTOTAL    FFLIGHT    FSCIENCE    FAIRTRAN
                  1      572       391         63          118
                  2      289       172         47           70
                  3      127        69         29           29

                                     (생략)

                 19      192        44         20          128
                 20      247        70         23          154
                 21      299       107         26          166

<br>

## 변수 이름 바꾸기  


```{}
<SAS PROGRAM>

DATA NASA2;
	SET NASA(rename=(total=to1 ftotal=to2));
RUN;
/* 같은 방법으로
DATA NASA2;
SET NASA;
RENAME total=to1 ftotal=to2;
run;
*/
proc print data=NASA2;
run;
```

▶ 지정된 변수를 새로운 변수로 바꾸는 방법이다.  


※ 변수 이름 바꾸기 (SET, RENAME)  

 • SET문을 이용하여 이미 작성된 DATASET을 토대로 새로운 이름의 DATASET 생성  

 • SET문에서 RENAME문을 사용하여 원하는 변수의 이름을 바꾸는 방법  

```{}
DATA 새로운 DSTASET명;
SET 기 생성된 DATASET명 (RENAME=(기 변수명 = 새로운 변수명 [기 변수명 = 새로운 변수명...]);
또는
DATA 새로운 DSTASET명;
SET 기 생성된 DATASET명
RENAME 기 변수명 = 새로운 변수명 [기 변수명 = 새로운 변수명...]);
```

    <OUTPUT>

                            NASA Expenditures : Facilities

      OBS YEAR  to1 PTOTAL PFLIGHT PSCIENCE PAIRTRAN to2 FFLIGHT FSCIENCE FAIRTRAN
        1 1966 5933  5361    3819    1120      422   572   391      63       118
        2 1967 5426  5137    3477    1160      500   289   172      47        70
        3 1968 4726  4599    3028    1061      510   127    69      29        29
        4 1969 4252  4187    2754     893      540    65    27      21        17
        5 1970 3753  3699    2195     963      541    54    14      21        19
   
                                          (생략)

       14 1979 4187  4054    2175    1144      735   133    41       9        83
       15 1980 4850  4710    2556    1341      813   140    38       5        97
       16 1981 5241  5274    3026    1380      868   147    26       4       117
       17 1982 6035  5926    3526    1454      946   109    17       3        89
       18 1983 6664  6556    4027    1486     1043   108    26       .        82
       19 1984 7048  6856    4037    1667     1152   192    44      20       128
       20 1985 7251  7001    3852    1834     1318   247    70      23       154
       21 1986 7404  7105    3787    2101     1317   299   107      26       166

  
▶ total 변수가 to1 변수명으로 ftotal 변수가 to2 변수명으로 바뀐 것을 알 수 있다.  
