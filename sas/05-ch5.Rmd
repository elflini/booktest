# 새로운 변수의 생성  {#chapter5}


## PROC PRINT 형식 2  


```{}
<SAS PROGRAM>

DATA travel;
     INPUT country $ nights aircost landcost vendor $;
CARDS;
France 8 793 575 Major
Spain 10 805 510 Hispania
India 10 . 489 Royal
Peru 7 722 590 Mundial
;
RUN;

PROC PRINT DATA=travel;
    TITLE 'DATA SET Travel';
RUN;

PROC PRINT DATA=travel(firstobs=2 obs=3) noobs double label N uniform;
    TITLE 'DATA SET Travel';
RUN;
```

▶ 문자형 관찰값이 있는 경우 문자 변수를 읽는 방법  
 - 문자변수로 인식 : INPUT문에서 변수명 다음에 $을 써줌 ex) country, vendor  
 - 숫자변수로 인식 : 그냥 변수명만 나열 ex) nights, aircost, landcost  

▶ 칼럼과 공백  
 - INPUT문에서 변수명들 다음에 시작 칼럼과 끝 칼럼을 지정하지 않은 경우  
   : CARDS문 다음에 오는 관찰값은 공백으로 분리  
 - 결측값이 있는 경우 : .으로 표시  
 - 결측값을 공백으로 처리 : 변수명 뒤에 시작칼럼과 끝 칼럼을 지정  
     ex) INPUT country $ 2-7 nights 9-10 aircost 13-15 landcost 18-20 vendor $ 22-29;  
       = INPUT country $ nights aircost 13-15 landcost vendor $;  

    <OUTPUT>

                            DATA SET Travel                            

      Obs    country    nights    aircost    landcost    vendor

       1     France        8        793         575      Major
       2     Spain        10        805         510      Hispania
       3     India        10          .         489      Royal
       4     Peru          7        722         590      Mundial
       
<br>

                            DATA SET Travel       

        country    nights    aircost    landcost    vendor

           Spain       10        805         510      Hispania
           India       10          .         489      Royal
                            관측치 개수 = 2

※ PROC PRINT의 형식  

 • SAS DATASET에 있는 내용을 출력하는 PROCEDURE  

 • DATA=출력하고자 하는 DATASET명, 지정하지 않으면 가장 최근에 생성된 DATASET 이용  

 • 인쇄되는 관찰값의 시작과 끝을 지정. FIRSTOBS=다음에 오는 숫자 (n1)번, OBS=다음의 숫자(n2).물론 이를 생략하면 DATASET의 처음부터 끝까지 출력된다.  
 • DOUBLE은 출력할 때 빈칸을 두 칸 띄며, NOBOS는 OBS번호를 출력하지 않고, LABEL은 출력 시 변수명 대신에 해당 LABEL이 출력된다.  

 • N은 끝에 해당 DATASET의 수를 출력하고, UNIFORM은 페이지 당 출력되는 변수의 관찰값들을 지정한다.  

```{}
PROC PRINT [DATA=dataset명[(FIRSTOBS=n1 oBS=n2)][DOUBLE][NOOBS][LABEL][N][UNIFORM];
  VAR 변수명들 ;	    출력할 변수의 지정, 생략하면 모든 변수 출력
  ID 변수명 ;	        OBS 변호대신에 지정 변수값이 출력
  BY 변수명들 ;	      BY 다음의 변수에 의해 정렬되어 있는 경우 각 수준별로 출력
  PAGEBY 변수명 ;	    PAGEBY다음의 변수의 값이 바뀔 때마다 새 페이지에 출력
  SUM 변수명들 ;	    지정된 변수의 합계가 출력된다.
  SUMBY 변수명들 ;	  BY나 SUMBY변수의 값이 변할 때마다 합계 출력
RUN;
```

<br>

## 새로운 변수 만들기  


```{}
<SAS PROGRAM>

DATA newair;
     SET travel;
     newcost=aircost+10;
RUN;
PROC PRINT DATA=newair;                
   VAR country aircost newcost;
   TITLE 'Increasing the Air Fare by $10 in All Observations';
RUN;
```

▶ 이미 생성된 DATASET을 이용하여 새로운 변수를 추가하여 새로운 DATASET을 만드는 방법   
 1) Newair DATASET을 살펴보면 앞에서 만들어진 travel DATASET의 모든 변수들을 취하면서 aircost라는 변수를 이용하여 생성된 newcost변수가 1개 더 있게 된다.  
 2) 이미 있는 변수를 조작하여 새로운 변수를 만들고자 할 경우  
   - 새로이 생성될 변수명 = 원하는 형태의 식  
     이 때 사용되는 수식은 일반 사칙연산에서부터 SAS에 내장된 함수를 이용하여 원하는 수식을 구현한다.  
 3) 사용할 수 있는 위치  
   - CARDS문이 있는 경우 : DATA문 다음 CARDS문 전에 위치  
   - CARDS문이 없는 경우 : PROCEDURE가 나오기 전단계의 RUN문 앞에 위치  
     현재 DATASET에 적용되고 그 외의 위치에 사용하면 오류가 발생하게 된다.  
 4) 예  
  - newcost변수 : 이미 있는 aircost변수의 관찰값에 각각 10을 더하여 생성한다. 사용위치는 DATA newair; 다음 PROC PRINT가 실행되기 전단계의 RUN; 앞에 위치   
  - 결과 : newair DATASET에는 country, nights, aircost, landcost, vendor변수와 aircost변수의 관찰값에 10을 더해서 만들어진 newcost변수가 존재한다.  
 5) PRINT PROCEDURE  
  - newair DATASET을 VAR문을 사용하지 않으면 있는 변수가 전부 출력되나 위에서는 VAR문 다음에 변수를 지정하였으므로 전체 변수들 중 해당 변수만 출력한다.  

    <OUTPUT>

            Increasing the Air Fare by $10 in All Observations          

                  Obs    country    aircost    newcost

                   1     France       793        803
                   2     Spain        805        815
                   3     India          .          .
                   4     Peru         722        732


▶ aircost변수의 3번째 관찰값이 결측값이었으므로 aircost변수에 10을 더하여 생성된 newcost변수의 관찰값도 역시 결측값으로 인식한다.  


※ 새로운 변수 만들기  

 • 이미 존재하는 변수를 이용하여 새로운 변수를 만들 수가 있으며, 만드는 방법은 사칙연산은 물론이고 SAS에서 지원되는 여러 가지 함수를 가자고 수식을 쓰는 형태와 마찬가지로 만들 수 있다.  

 새로운 변수명 = 수식 ;  


※ 기본 사칙 연산 : +, -, *, / (가감승제), **(제곱) 등  
※ 지원되는 SAS 내장함수  
 • ABS - 절대값                            • SQRT - 제곱근  
 • DIM - ARRAY문 사용할 때 원소의 개수   • EXP - 지수함수  
 • MOD - 나눗셈에서 나머지 정수값         • LOG - 자연로그  

예) x=SQRT(y1)+2\*EXP(y2)\*LOG(y3)/(ABS(y4) ;  

$x=\sqrt{y_1}+2\times e^{y_2}\times log{y_3}/|y_4|$라는 식을 SAS 수식으로 표기한 것이다.  
즉 변수 에는 변수의 제곱근에 변수의 지수값에 2를 곱하고 변수에 자연로그를 취하여 곱한 후에 변수에 절대값을 취하여 나눈 값으로 형성된다.  

<br>

## IF문의 형식  


```{}
<SAS PROGRAM>

DATA bonus;
     SET travel;
     IF vendor='Hispania' THEN bonuspts='For 10+ people';
     ELSE IF vendor='Mundial' THEN bonuspts='Yes';
RUN;
PROC PRINT DATA=bonus;
   VAR country vendor bonuspts;
   TITLE 'The SAS System Creates a VARiable for All Observations';
RUN;
```

▶ IF~THEN문, ELSE IF ~ THEN문이 사용되는 목적  
  1) 어떤 조건식에 맞는 경우 새로운 변수를 만들 경우  
  2) 기존의 값을 변화시키고자 하는 경우  

▶ Bonus DATASET   
 - travel DATASET의 모든 변수와 관찰값을 이용하여 vendor 변수의 특정값을 갖는 관찰값에 대해 새로운 변수 bonuspts에 특정값을 지정한다.  
   ex) vendor 변수의 값 : 'Hispanis'이면 bonuspts변수에 ‘For 10+people'  
   vendor 변수의 값 : 'Mundial'이면 bonuspts변수에는 ‘Yes'  
   vendor 변수의 값 : 'Hispanis'과 'Mundial'이 아닌 나머지에 대해서는 bonuspts 변수의 값은 MISSING이 됨  

▶ IF문의 형식은 IF 조건식 THEN 실행문 ; 으로 구성되며 조건식이 참이 되면 실행문을 수행한다. IF문이 같은 변수에 대해 연속적으로 사용될 때 ELSE IF 문을 사용하면 편리하다.  


※ IF문에서 사용되는 조건식  
= 같다  
^= 같지 않다  
>= 크거나 같다  
<= 작거나 같다  
< 작다  
> 크다  
AND 또는 & 논리곱  
OR 또는 |  논리합  
NOT 아니다  

    <OUTPUT>

         The SAS System Creates a VARiable for All Observations        

              Obs    country    vendor      bonuspts

               1     France     Major
               2     Spain      Hispania    For 10+ people
               3     India      Royal
               4     Peru       Mundial     Yes


▶ vendor 변수의 값 : 'Hispanis'인 2번째 관찰값의 경우 bonuspts변수는 ‘For 10+people'   
   vendor 변수의 값 : 'Mundial'인 4번째 관찰값의 경우 bonuspts변수에 ‘Yes', 그 외의 것인 1, 3번째 관찰값의 경우에는 vendor변수의 값이 아무런 작업을 행하지 않았으므로 결측값을 가지는 것을 볼 수 있다.  


※ IF문의 형식  

 • IF문은 어떤 조건식에 맞는 경우 새로운 변수를 만들거나 기존의 값을 변화시키고자 하는 경우에 사용된다.  

① IF 조건식 ;			조건식에 맞는 것(참인 것)만 취한다.  

② IF 조건식 THEN 실행문 ;	조건식에 맞는 것만 실행문을 행한다.  

③ IF 조건식1 THEN 실행문1 ;   
   ELSE IF 조건식2 THEN 실행문2 ;  
  ⟹ 조건식1에 맞으면 실행문1을 행하고, 틀리면 조건식2가 조건에 맞는지를 보고 실행문2를 실행한다. 조건식1과 2가 전부 거짓이 되는 관찰값은 아무 실행문도 행하지 않게 된다.   

④ IF 조건식1 THEN 실행문1 ;   
   ELSE IF　조건식2 THEN 실행문2 ;  
   ELSE 실행문3 ;  
  ⟹ 조건식 1과 2의 경우에 대해 참인 것은 ③의 경우와 같으며, 조건식1과 2에 대해 모두 거짓이면 실행문3을 행한다.  
  
<br>

## 변수의 값 바꾸기  


```{}
<SAS PROGRAM>

DATA newair2;
     SET travel;
     aircost=aircost+10;
RUN;
PROC PRINT DATA=newair2;               
   VAR country aircost;
   TITLE 'Changeing the InFORMATion in a VARiable';
RUN;
```

▶ SAS DATASET에 존재하는 변수들의 값을 변화시킨다.  
 1) 기존에 존재하는 변수 = 원하는 형태의 식  
 2) 위에서 Newair2 DATASET : travel DATA의 변수를 이용하면서 aircost변수의 값들에 일정값을 더하여 만든다.  
           aircost=aircost+10 : travel DATASET에서 aircost의 값이 20이라면 newair DATASET의 aircost 값은 30이 된다.  
 3) 관찰값 중 결측값이 있는 경우에는 특정값을 더하여도 결측값으로 존재한다.  
    ex) travel DATASET에서 aircost변수의 3번째 관찰값이 결측값이면 newair2 DATASET의  aircost변수의 값 또한 결측값이 된다.  
 4) 결측값이 안되기 위해서는 SAS에서 제공되는 함수를 이용한다.  
    - aircost=SUM(aircost, 10);이라고 하면 원래 aircost값이 결측값인 경우 계산 후 aircost 변수값은 10이 된다.  

    <OUTPUT>

              Changeing the InFORMATion in a VARiable                

                       Obs    country    aircost

                        1     France       803
                        2     Spain        815
                        3     India          .
                        4     Peru         732


▶ travel DATASET : aircost 793, 805, ., 722   
                                    → 
   newair2 DATASET : aircost 803, 815, ., 732  


※ 변수의 값 바꾸기  

 • 이미 존재하는 변수의 값을 변환시킬 수 있다. 이것은 새로운 변수를 생성하는 방법과 수식을 사용하는 방법은 같으나 왼쪽에 기존에 존재하는 변수명이 온다는 것만 다르다  

 • 변수의 값을 적당한 수식을 이용하여 변경할 경우, 관찰값중 결측값이 있는 경우에 변경된 값 역시 결측값이다.  

```{}
  기존의 변수명 = 수식 ;  
```

<br>

## 변수의 관찰값 길이 정하기


```{}
<SAS PROGRAM>

DATA newstmt;
     SET travel;
     LENGTH remarks $ 30;
     IF vendor='Hispania' THEN remarks='Bonus for 10+ people';
     ELSE IF vendor='Mundial' THEN remarks='Bonus points';
     ELSE IF vendor='Major' THEN remarks='Discount: 30+ people';
RUN;

PROC PRINT DATA=newstmt;              
   VAR country vendor remarks;
   TITLE 'Using a LENGTH Statement';
RUN;
```

▶ IF ~ ELSE IF 문을 이용하여 문자값을 가지는 새로운 변수를 생성하는 프로그램  
  - vendor 값 ‘Hispania' : remarks 변수 값 'Bonus for 10+ people'  
  'Mundial'                   'Bonus points'  
  'Majors'                    'Discount: 30+ people'  
  - 그 외의 경우 remarks변수의 값은 결측값을 가진다.  
  - ELSE IF 대신에 IF를 써도 무방하나 ELSE IF를 사용하는 것이 비교하는 횟수를 줄이므로 프로그램이 실행되는데 효율적이다.  

▶ LENGTH 다음에 해당변수와 자릿수를 지정하면 그 변수의 관찰값은 자릿수만큼만 나오고 그 이상일 경우 뒷부분이 삭제된다.  

    <OUTPUT>

                   Using a LENGTH Statement                       

           Obs    country    vendor      remarks

            1     France     Major       Discount: 30+ people
            2     Spain      Hispania    Bonus for 10+ people
            3     India      Royal
            4     Peru       Mundial     Bonus points


▶ remarks변수의 관찰값의 길이를 지정한 것이다. remarks변수의 관찰값 중 30자가 넘는 관찰값이 존재하였다면 출력결과가 다르지만 위의 경우에는 30자 안쪽이므로 같은 출력결과를 가지게 된다.  


※ 변수의 관찰값 길이 정하기  

 • 문자변수의 경우에 관찰값의 길이는 LENGTH문을 이용하여 원하는 길이로 지정할 수 있다.  

 • 지정된 자릿수보다 관찰값의 길이가 작을 경우는 관찰값이 그대로 나타나지만 관찰값의 길이가 더 길 경우에 오른쪽이 절단되어 나타난다.  

```{}
 LENGTH 문자변수명 $ 자릿수 ;  
```

<br>

## 특정 관찰값들을 삭제하기 (IF, DELETE문)  


```{}
<SAS PROGRAM>

DATA subset;
     SET travel;
     IF country='Peru' THEN delete;
RUN;
PROC PRINT DATA=subset;                   
      TITLE 'Omitting a Discontinued Tour';
RUN;
```

▶ IF문과 DELETE를 이용하여 특정 관찰값에 해당되는 행들을 삭제한다.  
 ex) Subset DATASET : travel DATASET의 변수는 그대로 가지면서 관찰값들 중country변수의 값 중 ‘Peru'를 가지면 그 행은 삭제한다.  

▶ 특정 조건에 맞는 관찰값의 행을 삭제할 경우  
 1) IF문 사용 : IF문 다음에 조건 THEN DELETE ; 조건에 맞는 행은 삭제  
 조건에 맞는 관찰값들이 있는 경우에 삭제되므로 관찰값들의 개수는 같거나  더 작게 된다.  
 2) ex) travel DATASET 경우 : 관찰값들의 개수는 4개  
       subset DATASET 경우 : country변수의 관찰값이 ‘Peru'인 1개가 삭제되면서 3개의 관찰값들만 가짐  
       문자변수인 경우 : ' ' 안에 넣어야 함  
       숫자변수인 경우 : 생략  
 3) country 변수의 관찰값이 ‘Peru'인 것만 가지고 DATASET을 만들 경우  
   ① IF country = 'Peru' ;  
   ② IF country ^= 'Peru' THEN DELETE ;  
   ③ IF country='France' OR country='Spain' OR country='India' THEN DELETE  
       ← country 변수의 관찰값이 France, Spain, India, Peru만으로 구성된 경우 사용한다. 사용 가능 다른 관찰값이 있는 경우 OR을 이용해서 계속 나열한다.  
   ※ 특정 변수의 특정 값에 해당하는 DATASET을 구성할 경우 ①의 방법이 제일 간편하다.  
  4) 특정 관찰값들만 취하여 DATASET을 만드는 것은 전체 관찰값들 중 어떤 일부분만 취하여 분석하여야 할 경우 유용하다.  

    <OUTPUT>

                Omitting a Discontinued Tour                     

      Obs    country    nights    aircost    landcost    vendor

       1     France        8        793         575      Major
       2     Spain        10        805         510      Hispania
       3     India        10          .         489      Royal


▶ country변수의 관찰값중 ‘Peru'를 가지는 관찰값들(행)은 없는 것을 볼 수 있다.  
   해당변수의 값만 삭제되는 것이 아니라 모든 변수에 해당되는 값들 즉 해당 행이 삭제된 것을 볼 수 있음  


※ 특정 관찰값들을 삭제하기 (IF 와 DELETE 사용)  

 • IF문을 이용하여 특정 조건에 맞는 관찰값의 행을 삭제할 수 있다.  

 • 조건식을 지정할 경우 해당 관찰값은 ‘ ’ 안에 표시해 주어야 하며 숫자변수인 경우에 ‘ ’ 는 생략해도 상관없다.  

 • 조건식을 지정하는 경우 AND나 OR을 이용하여 여러 변수에 해당하는 값들을 각각 지정하거나 어느 한 변수에 대해 여러 가지 값을 지정하여 할 수 있다.  

 • 조건식을 지정할 경우 특정값을 제외하고 모두 삭제할 경우는 NOT을 이용하면 편리하다. 즉 변수명 ^= 관찰값 하면 된다. (마찬가지로 변수명=관찰값)  

 IF 조건식 THEN DELETE ;  
  ⟹ 조건식에 맞으면 삭제  

 IF 변수1 = ‘관찰값1’ AND 변수2=‘관찰값2’ THEN DELETE ;   
  ⟹ 변수1의 값이 ‘관찰값1’이고 변수2의 값이 ‘관찰값2’를 가질 경우 삭제됨  

 IF 변수1 ^=‘관찰값1’ OR 변수 2 ^=‘관찰값2’ THEN DELETE ;  
  ⟹ 변수1의 값이 ‘관찰값1’이거나 또는 변수2의 값이 ‘관찰값2’인 경우 삭제됨  

<br>

## 수식을 이용한 변수생성  


```{}
<SAS PROGRAM>

DATA tours;
     INPUT country $ 1-11 nights aircost landcost vendor $;
CARDS;
Japan          8 982 1020 Express
Greece       12   .   748  Express
New Zealand 16 1368 1539 Southsea
Ireland        7 787 628 Express
Venezuella    9 426 505 Mundial
Italy          8 852 598 Express
USSR        14 1106 1024 A-B-C
Switzerland   9 816 834 Tour2000
Australia     12 1299 1169 Southsea
Brazil         8 682 610 Almeida

PROC PRINT DATA=tours;
    TITLE 'DATA SET Tours';
RUN;
```

▶ Tours DATASET에서 주의사항 : country 변수의 관찰값이 빈 공백을 포함하고 있으므로 시작칼럼과 끝칼럼을 지정해야 한다.  


```{}
<SAS PROGRAM>

DATA newtour;
    SET tours;
    totcost=aircost+landcost;
    peakair=(aircost*1.10)+8;
    nitecost=landcost/nights;

PROC PRINT DATA=newtour;                            
   VAR country nights aircost landcost totcost peakair nitecost;
   TITLE 'New VARiables Containing Calculated Values';
RUN;
```
 
▶ Newtour DATASET - tours DATASET을 참조하여 totcost, peakair, nitecost변수가 새로 포함되었으며 이때, 기존 변수를 변화시켜 만든다.  
   ex) peakair : aircost 변수에 1.10을 곱한 후에 8을 더하여 생성하는데 순서가 곱하기    먼저 이므로 괄호가 없어도 연산결과는 같다  

    <OUTPUT>

                            DATA SET Tours                            

    Obs    country        nights    aircost    landcost    vendor

      1    Japan             8         982       1020      Express
      2    Greece           12           .        748      Express
      3    New Zealand      16        1368       1539      Southsea
      4    Ireland           7         787        628      Express
      5    Venezuella        9         426        505      Mundial
      6    Italy             8         852        598      Express
      7    USSR             14        1106       1024      A-B-C
      8    Switzerland       9         816        834      Tour2000
      9    Australia        12        1299       1169      Southsea
     10    Brazil            8         682        610      Almeida

<br>

                   New VARiables Containing Calculated Values

    Obs  country      nights  aircost  landcost  totcost  peakair  nitecost

      1  Japan           8       982     1020      2002    1088.2   127.500
      2  Greece         12         .      748         .        .     62.333
      3  New Zealand    16      1368     1539      2907    1512.8    96.188
      4  Ireland         7       787      628      1415     873.7    89.714
      5  Venezuella      9       426      505       931     476.6    56.111
      6  Italy           8       852      598      1450     945.2    74.750
      7  USSR           14      1106     1024      2130    1224.6    73.143
      8  Switzerland     9       816      834      1650     905.6    92.667
      9  Australia      12      1299     1169      2468    1436.9    97.417
     10  Brazil          8       682      610      1292     758.2    76.250


▶ 2번째 행의 경우 aircost변수의 값이 결측값이므로 생성된 totcost와 peakair변수의 값  역시 결측값이다.  

<br>

## ROUND, SUM 함수를 이용한 새로운 변수의 생성  

```{}
<SAS PROGRAM>

DATA moretour;
    SET tours;
    roundair=ROUND(aircost, 50);
    totcostr=ROUND(aircost+landcost, 100);
    sumcost=SUM(aircost, landcost);
    roundsum=ROUND(SUM(aircost, landcost), 100);

PROC PRINT DATA=moretour;                     
    VAR country aircost landcost roundair totcostr sumcost roundsum;
RUN;
```

▶ SAS 내장함수를 이용하여 새로운 변수를 생성하는 방법  
 1) moretour DATASET  
  - DATASET의 변수들과 새로이 생성된 roundair, totcostr, sumcost, roundsum 변수로 구성된다.  
  - 변수를 새로이 생성할 때 반올림에 이용되는 ROUND함수와 합계를 구하는 SUM함수를 이용한다.  
 2) ROUND함수  
  - ROUND(변수, 반올림 숫자)의 형식으로 사용  
    ① (반올림숫자 ± 반올림숫자/2) : 사이에 있으면 반올림 숫자  
    ② (반올림숫자 - 반올림숫자/2) : 작은 경우 버림  
    ③ (반올림숫자 + 반올림숫자/2) : 크면 반올림     
  - roundair=ROUND(aircost, 50)   
    ① aircost변수의 값중 마지막 두 자리의 수가 25에서 75 사이인 경우 :  
       roundair변수의 값의 마지막 두 자리는 50의 값을 가지게 됨  
    ② aircost변수의 값중 마지막 두 자리가 25미만일 경우 : 00  
    ③ aircost변수의 값중 마지막 두 자리가 75이상일 경우 : 100  
  - Ex)  
    ① aircost변수의 관찰값 235 : roundair변수의 관찰값 250  
    ② aircost변수의 관찰값 389 : roundair변수의 관찰값 400    
 3) SUM함수  
  - ( )안에 사용된 변수들의 관찰값별로 합을 구하는 내장함수  
  - Sumcost 변수  
    ① SUM(aircost, landcost)라고 하여 aircost와 lndcost변수의 합으로 만들어짐  
    ② sumcost=aircost+landcost ; 두 변수의 합으로 sumcost라는 새로운 변수가 생성되나 두 변수들의 관찰값 중 결측값이 존재한다면 sumcost변수의 값은 서로 다른 값을 가짐  
  - SUM함수를 사용할 경우 주의사항  
    결측값이 어느 한 변수에 존재할 경우에도 나머지 변수의 관찰값들에 의해 합이 구해지지만, 사칙연산을 사용한 수식에서는 해당 변수에 결측값이 1개라도 존재하면 구해진 관찰값은 결측값으로 인식  
    
    <OUTPUT>

        New VARiables Containing Calculated Values              

    Obs country     aircost landcost roundair totcostr sumcost roundsum

      1 Japan          982    1020     1000     2000     2002    2000
      2 Greece           .     748        .        .      748     700
      3 New Zealand   1368    1539     1350     2900     2907    2900
      4 Ireland        787     628      800     1400     1415    1400
      5 Venezuella     426     505      450      900      931     900
      6 Italy          852     598      850     1500     1450    1500
      7 USSR          1106    1024     1100     2100     2130    2100
      8 Switzerland    816     834      800     1700     1650    1700
      9 Australia     1299    1169     1300     2500     2468    2500
     10 Brazil         682     610      700     1300     1292    1300

 
 1) roundair 변수의 경우 aircost의 마지막 두 자리의 값이 25-74이면 50, 00-24이면 00, 75-99이면 100이다.  
 2) totcostr변수와 roundair변수를 비교  
  - 반올림 숫자가 100이므로 50미만이면 00 50이상이면 100이다.  
  - 두 변수의 연산을 +를 이용한 경우 2번째 관찰값은 결측값을 가지지만 SUM함수를 이용하여 구한 것은 나머지 관찰값들의 합인 것을 볼 수 있다.  
  - 결측값이 없는 2번째 관찰값들을 제외한 나머지 관찰값들은 살펴보면 totcostr변수의 값과 roundsum변수의 값이 동일하지만 2번째 관찰값의 경우 + 를 사용한 totcostr변수의 값은 . 이지만 roundsum변수의 값은 748을 round시켜 나머지 두 자리가 50미만이므로 700의 값을 가지는 것을 볼 수 있다  


※ ROUND, SUM함수를 이용한 새로운 변수의 생성  

 • SAS 내장함수를 이용하여 새로운 변수를 생성할 수 있다.  

    새로운 변수 = ROUND(변수, 반올림 숫자);
      ⟹ (반올림숫자 ± 반올림숫자/2)의 값에 따라 버리거나 반올림한다.

    새로운 변수 = SUM (변수 변수 …) ; 
      ⟹ ()안의 변수들의 합을 행별로 구한다. 
      
* 참고  
1. 수학 계산 관련 함수  

|함수 형태|내용|예제|
|:----:|:------:|:------:|
|COS(argument)|COS값을 계산|V=cos(3.14159/3)|
|SIN(argument)|SIN값을 계산|V=sin(3*3.14159)|
|TAN(argument)|TAN값을 계산|V=tan(2)|
|EXP(변수명)|지수함수로 지수값을 계산|V=exp(3.2)|
|SQRT(변수명)|제곱근 값을 계산 <br> 제곱은 \**2 세제곱은 \**3|V=sqrt(3.2) <br> V=3\**2 V=3**3|
|LOG(변수명)|자연로그(natural log)값을 계산|V=log(3.2)|
|LOGN(변수명)|로그의 밑이 n인 로그값을 계산 <br> N=10이면 상용로그값|V=log10(3.2)|  

2. 통계 계산 관련 함수  

|함수형태|내용|예제|
|:----:|:------:|:-----:|
|FACT(n)|Factorial 값을 계산 <br> $n!$|V=fact(6)|
|COMB(n,r)|Combination 값을 계산 <br> $_nC_r=\frac{n!}{r!(n-r)!}$|V=comb(5,2)|
|PERM(n,r)|Permutation 값을 계산 <br> $_nP_r=\frac{n!}{(n-rP!}$|V=perm(5,2)|
|MAX(argument)|변수 관측치 최대값을 구함|V=max(x,y,z)|
|MIN(argument)|변수 관측치 최소값을 구함|V=min(x,y,z)|
|N(argument)|변수 관측치 개수(결측치 제외)을 구함|V=n(x,y,z)|
|SUM(argument)|변수 관측치 합을 구함|V=sum(x,y,z)|
|MEAN(argument)|변수 관측치 평균을 구함|V=mean(x,y,z)|
|RANGE(argument)|변수 관측치 범위를 구함|V=range(x,y,z)|
|STD(argument)|변수 관측치 표준편차를 구함|V=std(x,y,z)|
|STDERR(argument)|변수 관측치 표준오차를 구함|V=stderr(x,y,z)|
|VAR(argument)|변수 관측치 분산을 구함|V=var(x,y,z)|  

추가적인 사항은 APPENDIX 참조

    

