# IF문 사용하기  {#chapter6}


## IF ~ ; ELSE문  

    

```{}
<SAS PROGRAM>

DATA arttour;
	input city $ 1-9 nights 11 landcost 13-16 events 18
	      describe $ 20-36 guide $ 38-45 backup $ 47-54;
	cards;
Rome      3  750 7 4 M,  3 G         D'Amico  Torres  
Paris     8 1660 6 5 M,  1 other     Lucas    Lucas   
London    6 1230 5 3 M,  2 G         Wilson   Lucas   
New York  6    . 8 5 M,  1 G 2 other Lucas    D'amico 
Madrid    3  370 5 3 M,  2 other     Torres   D'amico 
Amsterdam 4  580 6 3 M,  3 G                  Vandever
;
proc print data=arttour;
	title 'Data Set ARTTOUR';
run;
```
  
▶ DATA의 구성 : city, nights, landcost, events, describe, guide, backup로 구성  

    

```{}
<SAS PROGRAM>

data revise;
	set arttour;
	if city='Rome' then landcost=landcost+30;
	if events>nights then calendar='Check schedule';
	if guide='Lucas' and nights>7 then guide='Torres';
run;
proc print data=revise;
	var city nights landcost events guide calendar;
	title 'Examples of IF-THEN Statements';
run;
```
 
▶ IF문을 이용하여 변수의 관찰값을 변경하거나, 조건에 맞는 경우만 특정변수의 값을 변경하거나 조건을 2개 이상 부여하는 것.   

    <OUTPUT>

                                Data Set ARTTOUR                           

    Obs city      nights landcost events describe           guide  backup

     1  Rome         3      750      7   4 M,  3 G         D'Amico Torres
     2  Paris        8     1660      6   5 M,  1 other     Lucas   Lucas
     3  London       6     1230      5   3 M,  2 G         Wilson  Lucas
     4  New York     6        .      8   5 M,  1 G 2 other Lucas   D'amico
     5  Madrid       3      370      5   3 M,  2 other     Torres  D'amico
     6  Amsterdam    4      580      6   3 M,  3 G                 Vandever

<br>

                         Examples of IF-THEN Statements                    
    
    Obs   city        nights   landcost   events    guide       calendar

     1    Rome           3        780        7     D'Amico   Check schedule
     2    Paris          8       1660        6     Torres
     3    London         6       1230        5     Wilson
     4    New York       6          .        8     Lucas     Check schedule
     5    Madrid         3        370        5     Torres    Check schedule
     6    Amsterdam      4        580        6               Check schedule


▶ TITLE이 ‘Examples of IF-THEN Statements'인 결과를 보면, city변수의 관찰값 'Rome'인  첫 번째 관찰값중 landcost변수의 값이 780으로 변화  
▶ events변수 값이 nightsqus수의 값보다 큰 1, 4, 5, 6번째의 관찰값의 calendar변수의 값이 'Check schedule'로 생성되고 조건이 맞지 않는 경우에 대해 결측값을 부여  

    

```{}
<SAS PROGRAM>

data revise2;
	set arttour;
	if city='Rome' then landcost=landcost+30;
	if events>nights then calendar='Check schedule';
	else calendar='No problems';
	if guide='Lucas' and nights>7 then guide='Torres';
run;
proc print data=revise2;
	var city nights landcost events guide calendar;
	title'Alternative Value Assigned by ELSE';
run;
```

▶ IF 조건식 THEN 실행문1; ELSE 실행문2; 형식  
   • 조건에 맞는 경우 실행문 1 수행, 그렇지 않는 경우 실행문 2를 수행한다.  
   • IF와 ELSE를 함께 사용하여 특정값을 지정할 때 유용하다.  


```{}
IF events>nights THEN calendar='Check schedule'; ELSE calendar='No problems';
  = IF events>nights THEN calendar='Check schedule';
       ELSE IF events<=nights calendar='No problems';
```

<br>

    <OUTPUT>

                       Alternative Value Assigned by ELSE                  

    Obs   city        nights   landcost   events    guide       calendar

     1    Rome           3        780        7     D'Amico   Check schedule
     2    Paris          8       1660        6     Torres    No problems
     3    London         6       1230        5     Wilson    No problems
     4    New York       6          .        8     Lucas     Check schedule
     5    Madrid         3        370        5     Torres    Check schedule
     6    Amsterdam      4        580        6               Check schedule



※ IF문 사용하기 1 (IF ~ ; ELSE 문)  

 • 조건에 맞는 경우 새로운 변수를 만들거나 기존 변수의 값을 바꿀수 있다.  

 • ELSE문과 같이 사용할 경우 ELSE 다음에 실행문을 쓰면 앞의 조건에 해당하는 값을 제외하고 나머지는 ELSE문 다음의 실행문이 된다.  

 • 조건식은 비교식과 논리식이 같이 공존할 경우에 비교식이 먼저 수행되고 나중에 논리식이 수행된다.  

 • 논리식의 경우 AND는 모든 비교식이 참이 될 때, OR은 비교식들중 하나 이상이 참일 경우 조건식은 참으로 인식한다.  

 IF 조건식 THEN 실행문 1; ESLE 실행문2;  
  ⟹ 조건식이 맞는 경우 실행문 1 수행 그 나머지 경우 실행문 2 수행  

<br>

## 연속자료의 범주화  

    

```{}
<SAS PROGRAM>

data prices;
	set arttour;
	if landcost>=1500 then price='High';
	else if landcost>=700 then price='Midium';
	else price='Low';
run;
proc print data=prices;
	var city landcost price;
	title'Mutually Exclusive Values';
run;
```

▶ IF문을 사용하여 기존 변수의 관찰값을 몇 개의 범주로 바꾸어 새로운 변수 생성  
    • landcost의 값들을 적절하게 범주로 나누어 price라는 변수를 생성한다.  

|landcost|price|
|:---:|:---:|
|1500이상|High|
|700~1500미만,|Medium|
|700미만|Low|  

   • 주의할 점 : 마지막에서 ELSE price='Low';라고 하였으므로 만약 landcost변수의 관찰   값 중 결측값이 있는 경우에 price변수의 관찰값은 ‘Low'가 됨   
   • 결측값이 없는 경우 다음과 같은 방법으로 사용해도 같은 결과가 나옴  

```{}
IF landcost<700 THEN price='Low';
ELSE IF landcost<1500 THEN price='Medium'; ELSE price='High';
```

<br>

    <OUTPUT>

                       Mutually Exclusive Values                       

                 Obs    city         landcost    price

                  1     Rome            750      Midium
                  2     Paris          1660      High
                  3     London         1230      Midium
                  4     New York          .      Low
                  5     Madrid          370      Low
                  6     Amsterdam       580      Low



※ IF문 사용하기 2 (IF ~ ; ELSE 문) : 연속자료의 범주화  

 • 연속형 자료를 이산형 자료 즉 범주화를 시킬수 있다.  

 • 조건식 자리에 비교 연산자를 이용하고 실행문 위치에 새로운 변수의 범주값을 넣으면 된다.  

```{}
 IF 조건식1 THEN 실행문1; ESLE IF 조건식2 THEN 실행문2;
  [ELSE IF 조건식3 THEN 실행문3; []]ELSE 실행문n ;  
```

<br>

## 조건식에서 여러 연산자 사용하기  

    

```{}
<SAS PROGRAM>

data changes;
	set arttour;
	if nights>=6 then stay='Week+';
	else stay='days';
	if landcost NE . then remarks='ok';
	else remarks='Redo';
	if landcost LT 600 then budget='Low';
	else budget='Medium';
	if events/nights>2 then pace='Too fast';
	else pace='ok';
run;
proc print data=changes;
	var city nights landcost events stay remarks budget pace;
	title 'Using Various Conditions';
run;
```
  
▶ 산술연산자, 비교연산자, 논리연산자 사용방법  
    • 비교연산자 : 기호로 사용하거나 약어로 사용해도 결과가 같다.  

|기호|>=|>|^=|<=|<|=|
|:---:|:---:|:---:|:---:|:---:|:---:|:---:|
|약어|GE|GT|NE|LE|LT|EQ|

  • 조건식의 자리에 산술연산자, 비교연산자가 같이 사용되었을 때 먼저 괄호로 묶인 부분이 수행되고 산술연산자, 비교연산자 순으로 실행되며 같은 연산자 내에서는 각기 연산순서가 빠른 것부터 수행된다.   

    <OUTPUT>

                          Using Various Conditions                       

     Obs city      nights landcost events stay  remarks budget pace

      1  Rome         3      750      7   days   ok     Medium Too fast
      2  Paris        8     1660      6   Week+  ok     Medium ok
      3  London       6     1230      5   Week+  ok     Medium ok
      4  New York     6        .      8   Week+  Redo   Low    ok
      5  Madrid       3      370      5   days   ok     Low    ok
      6  Amsterdam    4      580      6   days   ok     Low    ok

  
▶ landcost변수의 값이 결측값을 갖는 4번째 관찰값은 bugdget의 값이 ‘Low'인 것을 알 수 있다.  


※ IF문 사용하기 3 : 조건식에서 여러 연산자 사용하기  

 • 조건식에서 비교연산자 사용될 경우 기호나 약어를 사용.  

 • 산술연산자의 경우 연산순서는 괄호, 제곱, *나 /, +나 - 이며 왼쪽에서 오른쪽 방향  

예)   IF a^= 4000 THEN b=. ; 또는 IF a NE 4000 THEN b=. ;   
      IF x/f > 4000 THEN b = f ; 또는 IF x/f GT 4000 THEN b = f ;   

<br>

## 조건식에서 연산순서  

    

```{}
<SAS PROGRAM>

data showand;
	set arttour;
	if city='Paris' and guide='Lucas' then remarks='Bilingual';
	if 1000<=landcost<=1500 then price='1000-1500';
run;
proc print data=showand;
	var city landcost guide remarks price;
	title 'making multiple comparisons with AND';
run;
```

▶ 여러 연산자가 혼합해서 사용된 경우  
    • 연산순서 : 괄호 ⇨ 산술연산자 ⇨ 비교연산자 ⇨ 논리 연산자  
    • 같은 연산자 내에서는 각기 연산순서가 빠른 것부터 실행한다.  

    <OUTPUT>

                    making multiple comparisons with AND                 

     Obs    city         landcost     guide      remarks       price

      1     Rome            750      D'Amico
      2     Paris          1660      Lucas      Bilingual
      3     London         1230      Wilson                  1000-1500
      4     New York          .      Lucas
      5     Madrid          370      Torres
      6     Amsterdam       580

  
▶ remarks변수는 조건에 맞는 2번째 경우만 관찰값을 가지며 price변수의 경우도 조건에   맞는 3번째 경우에 대해서만 관찰값을 가진다.  

※ IF문 사용하기 4 : 조건식에서 연산순서  

 • 조건식의 자리에는 산술연산자, 비교연산자, 논리연산자가 같이 사용가능  

 • 연산순서 : 괄호 > 산술연산자 > 비교연산자 > 논리연산자의 순서  

 • AND : AND를 사이에 두고 양쪽에 위치하는 조건이 모두 참인 경우에 대해서만 참  

 • 비교연산자에서 범위 지정 시 AND를 이용하거나 직접 범위를 입력해도 됨  

```{}
   IF 조건문 1 AND 조건문 2 THEN 실행문1; 
    ⟹ 조건문 1이 참이고 조건문 2가 참이면 실행문 1이 수행
```

<br>

## 5. 여러 조건 사용하기  

    

```{}
<SAS PROGRAM>

data showor;
	set arttour;
	if landcost GT 1500 OR landcost/nights GT 200 
      then level='Deluxe';
run;
proc print data=showor;
	var city landcost nights level;
	title 'Making Multiple Compar isons with OR';
run;
```

▶ 논리 연산자 OR이 사용된 경우  
    • IF 조건식은 비교 연산자 결과가 참인지 거짓인지 확인 후 OR을 사용하기 때문에 하나라도 참으로 판명이 되면 THEN 이하의 실행문을 수행한다.  

   

```{}
 <SAS PROGRAM>
 
 data test;
	set arttour;
	if guide NE backup OR guide NE'' then use_or='OK';
	else use_or='No';
	if guide NE backup AND guide NE'' then use_and='OK';
	else use_and='No';
run;
proc print data=test;
	var city guide backup use_or use_and;
	title 'Negative Operators with OR';
run;
```

▶ 논리 연산자 AND와 OR을 사용한 경우  

    <OUTPUT>

                  Making Multiple Compar isons with OR                 

            Obs    city         landcost    nights    level

             1     Rome            750         3      Deluxe
             2     Paris          1660         8      Deluxe
             3     London         1230         6      Deluxe
             4     New York          .         6
             5     Madrid          370         3
             6     Amsterdam       580         4


▶ 두 조건중 하나만 참이면 되므로 1, 2, 3번째 관찰값의 level변수는 ‘Deluxe'가 된다.  

    <OUTPUT>

                       Negative Operators with OR                      

      Obs    city          guide     backup      use_or    use_and

       1     Rome         D'Amico    Torres        OK        OK
       2     Paris        Lucas      Lucas         OK        No
       3     London       Wilson     Lucas         OK        OK
       4     New York     Lucas      D'amico       OK        OK
       5     Madrid       Torres     D'amico       OK        OK
       6     Amsterdam               Vandever      OK        No


▶ use_or 과 use_and의 값이 다른 것을 알 수 있다.  

    

```{}
<SAS PROGRAM>

data combine;
	set arttour;
	if (city='Paris' or city='Rome') AND 
         (guide='Lucas' or guide="D'Amico") 
    then topic='Art history';
run;
proc print data=combine;
	var city guide topic;
	title 'Grouping Comparisons with Rarentheses';
run;
```

▶ 조건이 많고 여러 연산자가 혼합되어 있는 경우  
    • ( )를 이용하여 먼저 수행해야 할 부분을 지정하여 조건식을 만들어 참인 경우에 새로운 변수에 값을 부여한다.  

    

```{}
<SAS PROGRAM>

data morecomp;
	set arttour;
	if landcost then remarks='Ready to budget';
	else remarks='Need land cost';
	if nights=6 or nights=8 then stay='Medium';
	else stay='Short';
run;
proc print data=morecomp;
	var city nights landcost remarks stay;
	title 'More About Numeric Comparisons';
run;
```
  
▶ 해당변수가 관찰값을 가지는 경우는 ‘참’으로 인식, 결측값을 가지는 경우는 ‘거짓’으로 인식한다.  

    <OUTPUT>

                 Grouping Comparisons with Rarentheses                 

               Obs    city          guide        topic

                1     Rome         D'Amico    Art history
                2     Paris        Lucas      Art history
                3     London       Wilson
                4     New York     Lucas
                5     Madrid       Torres
                6     Amsterdam


▶ city변수가 ‘Paris or Rome’이고 guide변수가 ‘Lucas or D'Amico’경우 topic변수는 ‘Art history’값을 가지고 나머지는 결측값을 가진다.  

    <OUTPUT>

                       More About Numeric Comparisons                    10
  
    Obs    city         nights    landcost        remarks         stay

     1     Rome            3         750      Ready to budget    Short
     2     Paris           8        1660      Ready to budget    Medium
     3     London          6        1230      Ready to budget    Medium
     4     New York        6           .      Need land cost     Medium
     5     Madrid          3         370      Ready to budget    Short
     6     Amsterdam       4         580      Ready to budget    Shor


▶ landcost변수가 결측값일 때 remarks변수는 ‘Need land cost'로 나타나고, stay변수의 경우 nights변수값이 6 or 8인 경우만 'Medium'값을 가지고 나머지는 'Short'값이다.  

    

```{}
<SAS PROGRAM>

data newguide;
	set arttour;
	if upcase(city)='MADRID' then guide='Duncan';
run;
proc print data=newguide;
	var city guide;
	title'Using the UPCASE Function to Make a Comparison';
run;
```

▶ UPCASE함수를 이용하여 조건에 맞는 경우 특정변수의 관찰값을 바꾼다.  
    • UPCASE함수는 주로 어떤 변수의 값을 변화시키는데 이용되는 함수이다.  

    

```{}
<SAS PROGRAM>

data dguide;
	set arttour;
	if guide=:'D' then chosen='Yes';
	else chosen='No';
run;
proc print data=dguide;
	var city guide chosen;
	title 'Guides Whose Nnmes Begin with D';
run;
```

▶ 조건식에서 특정 문자로 시작하는 경우를 찾는 것.  
    • 특정 문자로 시작되는 경우를 지정할 때 변수= : ‘특정문자’로 지정한다.  

    <OUTPUT>

             Using the UPCASE Function to Make a Comparison            

                      Obs    city          guide

                       1     Rome         D'Amico
                       2     Paris        Lucas
                       3     London       Wilson
                       4     New York     Lucas
                       5     Madrid       Duncan
                       6     Amsterdam


▶ city변수의 값이 ‘MADRID'인 5번째 관찰값의 경우 guide변수의 값은 arttour dataset에서는 'Torres'이었는데 'Duncan'으로 바뀐 것을 알 수 있다.  

    <OUTPUT>

                    Guides Whose Nnmes Begin with D                    

                 Obs    city          guide     chosen

                  1     Rome         D'Amico     Yes
                  2     Paris        Lucas       No
                  3     London       Wilson      No
                  4     New York     Lucas       No
                  5     Madrid       Torres      No
                  6     Amsterdam                No


▶ guide변수의 관찰값중 'D'로 시작하는 관찰값은 1번째만 해당하므로 chosen변수의 값이 'Yes'이고, 나머지는 'No'값을 가지는 것을 볼 수 있다.  

    

```{}
<SAS PROGRAM>

data guidegrp;
	set arttour;
	if guide<=:'L' then group='A-L';
	else group='M-Z';
run;
proc print data=guidegrp;
	var city guide group;
	title'All Values Beginning with a Range of Charcaters';
run;
```

▶ 특정 문자로 시작되는 관찰값을 찾아 그룹을 지정하는 것  
    • 숫자의 경우 : 비교 연산자와 IF문 이용  
    • 문자의 경우 : 비교 연산자 뒤에 :을 함께 사용하여 아스키값이 그 이하인지 이상인지를 인지하는 것  

    <OUTPUT>

            All Values Beginning with a Range of Charcaters           13

                  Obs    city          guide     group

                   1     Rome         D'Amico     A-L
                   2     Paris        Lucas       A-L
                   3     London       Wilson      M-Z
                   4     New York     Lucas       A-L
                   5     Madrid       Torres      M-Z
                   6     Amsterdam                A-L


▶ guide변수의 값이 A에서 L까지의 group변수 값은 A-L이 되고, M에서 Z까지는 group변수값이 M-Z가 되는 것을 볼 수 있다.  

<br>

## 특정 관찰값 삭제  

    

```{}
<SAS PROGRAM>

data arts; 
   input city $ 1-9 nights 11 landcost 13-16 budget $ 18-23 guide $ 25-32 ;
cards ;
Rome      3  750 Medium D'Amico 
Paris     8 1680 High   Lucas 
London    6 1230 High   Wilson 
New York  6    .        Lucas 
Madrid    3  370 Low    Torres 
Amsterdam 4  580 Low 
;
run ;
proc print data=arts; 
   title 'Data Set ARTS' ;
run ;
```

▶ Arts DATASET은 city, nights, landcost, budget, guide등 5개의 변수와 6개의 관찰값으로 형성  

    <OUTPUT>

                              Data Set ARTS                            

      Obs    city         nights    landcost    budget     guide

       1     Rome            3         750      Medium    D'Amico
       2     Paris           8        1680      High      Lucas
       3     London          6        1230      High      Wilson
       4     Newyork         6           .                Lucas
       5     Madrid          3         370      Low       Torres
       6     Amsterdam       4         580      Low


▶ 문자변수의 값이 결측값을 가질 경우 : 공백  
   숫자변수의 값이 결측값을 가질 경우 : .  

    

```{}
<SAS PROGRAM>

data remove; 
set arts; 
if landcost=. then delete ;
run ;
proc print data=remove; 
title 'Deleting Observations That Lack a Land Cost' ;
run ;
```

▶ 기 생성된 DATASET을 가지고 특정변수의 값이 결측값을 가질 경우 그 관찰값을 지운다.  
 - remove DATASET은 이미 생성된 arts DATASET을 이용하여 landcost변수의 값이 결측값을 가질 경우 그 관찰값을 제외시키고 DATASET을 형성한다.  

    <OUTPUT>

            Deleting Observation That Lack a Land Cost              2

      Obs    city         nights    landcost    budget     guide

       1     Rome            3         750      Medium    D'Amico
       2     Paris           8        1680      High      Lucas
       3     London          6        1230      High      Wilson
       4     Madrid          3         370      Low       Torres
       5     Amsterdam       4         580      Low


▶ landcost 변수의 값이 결측값을 가지는 4번째 관찰값이 제외된다.  

<br>

## 특정 관찰값 선택  

    

```{}
<SAS PROGRAM>

data remove2; 
input city $ 1-9 nights 11 landcost 13-16 budget $ 18-23 guide $ 25-32 ;
if landcost=. then delete ;
cards ;
Rome 3 750 Medium D'Amico 
Paris 8 1680 High Lucas 
London 6 1230 High Wilson 
New York 6 . Lucas 
Madrid 3 370 Low Torres 
Amsterdam 4 580 Low 
;
run ;
proc print data=remove2; 
title 'Deleting Observations While Inputting Them' ;
run ;
```

▶ IF문을 사용하는 위치는 CARDS문 앞에 위치  

    <OUTPUT>

               Deleting Observation While InPUTting Them              

      Obs    city         nights    landcost    budget     guide
       1     Rome            3         750      Medium    D'Amico
       2     Paris           8        1680      High      Lucas
       3     London          6        1230      High      Wilson
       4     Madrid          3         370      Low       Torres
       5     Amsterdam       4         580      Low


▶ landcost변수의 값이 결측값일 경우에는 삭제하고 DATASET이 형성된다.  
▶ 이미 생성된 art DATASET을 이용하여 새로운 subset6 DATASET을 형성하는데 특정 조건에 맞는 관찰값만 취하는 방법이다.  

    

```{}
<SAS PROGRAM>

data subset6; 
set arts; 
if nights=6 ;
run ;
proc print data=subset6; 
title 'Producing a Subset with the Subsetting IF Statement' ;
run ;
```

▶ Subset6 DATASET에서 보면 IF nights=6; 이라고 하여 nights 변수의 값이 6인 것만    참이 되고 나머지는 거짓이 되므로 nights변수값인 6인것만 취하여 subset6 DATASET을 형성한다.  

    <OUTPUT>

           Producing a Subset with the Subsetting IF Statement          

        Obs     city      nights    landcost    budget    guide
         1     London        6        1230       High     Wilson
         2     Newyork       6           .                Lucas


▶ subset6 DATASET에는 nights변수의 값이 6인 관찰값만을 가지게 된다.  


※ if 문 사용하여 관찰값 선택  

 • IF문 이용하여 원하는 관찰값만 취하여 DATASET을 만들 수 있다.  
 • IF 조건식; - 조건식이 참이 되는 관찰값만 취함  
 
<br>

## UPCASE함수를 이용한 특정 관찰값 선택  

▶ UPCASE함수를 이용하여 변수값중 특정값을 가지는 관찰값들을 지우는 것  

    

```{}
<SAS PROGRAM>

data lowmed; 
set arts; 
if upcase(budget)='HIGH' then delete ;
run ;
proc print data=lowmed; /*output5*/
title 'Deleting High-Priced Tours' ;
run ;
```

▶ Lowmed DATASET의 내용은 budget변수의 값이 ‘HIGH'인 관찰값들은 제거하고
   나머지 관찰값들만 가지고 생성 (결측값인 경우 통합하여 생성)  

    <OUTPUT>

                          Deleting High-Priced Tours                      

      Obs    city         nights    landcost    budget     guide

       1     Rome            3         750      Medium    D'Amico
       2     Newyork         6           .                Lucas
       3     Madrid          3         370      Low       Torres
       4     Amsterdam       4         580      Low


▶ budget변수값이 ‘HIGH' 인 경우만 제외시키므로 budget변수값이 결측값을 포함한다.  

▶ budget변수값이 ‘MEDIUM'이나 ’LOW'인 경우만 선택하여 lowmed2 DATASET을 생성  

    

```{}
<SAS PROGRAM>

data lowmed2; 
set arts; 
if upcase(budget)='MEDIUM' or upcase(budget)='LOW' ;
run ;
proc print data=lowmed2; /*output6*/
title 'Using Subsetting IF for an Exact Selection' ;
run ;
```

▶ budget변수값이 ‘MEDIUM'이나 ’LOW'인 경우만 선택하여 lowmed2 DATASET 생성.  
   • budget변수값이 'MEDIUM', 'LOW'인 것만 가지고 DATASET을 만들 때  
   • IF UPCASE(budget)='HIGH' THEN delete; : 결측값 포함  
   • IF UPCASE(budget)='MEDIUM' OR IF UPCASE(budget)=‘LOW' : 결측값 제외  
     budget변수값이 'MEDIUM', 'LOW'인 것만 취하게 됨  

    <OUTPUT>

         Using Subsetting IF for an Exact Selection              

      Obs    city         nights    landcost    budget     guide

       1     Rome            3         750      Medium    D'Amico
       2     Madrid          3         370      Low       Torres
       3     Amsterdam       4         580      Low


▶ budget변수값이 'MEDIUM', 'LOW'인 것만 선택하므로 3개의 관찰값만을 가지게 됨  

<br>

## 다수의 DATASET만들기 (OUTPUT문)  

▶ 특정조건에 맞는 관찰값들을 취하여 두 개 이상의 새로운 DATASET을 만드는 것  
 - IF 조건식 THEN OUTPUT 새로운 DATASET명 ;   

    

```{}
<SAS PROGRAM>

data ltour othrtour; 
set arts; 
if guide='Lucas' then output ltour; 
else output othrtour; 
run ;
proc print data=ltour; 
title "Data Set with Guide='Lucas'" ;
run ;
proc print data=othrtour; 
title "Data Set with Other Guides" ;
run ;
```

▶ 특정조건에 맞는 관찰값들을 취하여 두 개 이상의 새로운 DATASET을 만드는 것  
 - arts DATASET을 이용하여 ltour, othrtour DATASET을 만든다.  
 - ltour DATASET에는 guide변수의 값이 ‘Lucas'인 것만 선택, othrtour은 ‘Lucas'를 제외한 나머지 모든 관찰값으로 이루어진다.   
 - guide변수의 값이 결측값을 가질 경우에도 othrtour DATASET에 통합된다.  

▶ 특정 값만 가지는 관찰값을 따로 분리해내는 방법은 IF문과 OUTPUT문을 사용  
 - IF조건식 THEN OUTPUT 새로운 DATASET명 ;   
   : **조건식에 맞는 관찰값들로 새로운 DATASET을 형성**  

▶ 두 개 이상 만들고자 할 경우  
 - ELSE IF 또는 ELSE문을 이용하여 계속 나열  
 - 단 새로운 DATASET명은 맨 처음 DATA문에 선언되어 있어야 가능하다.  


                         Data Set with Guide='Lucas'                      

        Obs     city      nights    landcost    budget    guide

         1     Paris         8        1680       High     Lucas
         2     Newyork       6           .                Lucas


▶ ltour DATASET을 출력한 것으로 Guide변수의 값을 보면 ‘Lucas'들로만 구성되어 있다.  


                           Data Set with Other Guides                      

      Obs    city         nights    landcost    budget     guide

       1     Rome            3         750      Medium    D'Amico
       2     London          6        1230      High      Wilson
       3     Madrid          3         370      Low       Torres
       4     Amsterdam       4         580      Low


▶ othrtour DATASET을 출력한 것으로 Guide변수의 값을 보면 ‘Lucas'가 아닌 값들과 결측값인 것으로만 구성되어 있다.  


※ 특정 관찰값으로 다수의 DATASET 만들기(IF, OUTPUT문)  

 • IF문과 OUTPUT문을 이용하여 원하는 관찰값만 취하여 두 개 이상의 새로운 DATASET을 만들 수 있다.  

```{}
DATA a1[a2 … ] aN ;		생성하고자 하는 DATASET수만큼 나열
  SET 기생성된 DATASET명 ;  
     …                        	다른 프로그램
IF 조건식 1 THEN OUTPUT a1 ;
   [ELSE IF 조건식2 THEN OUTPUT a2 ; … ]
    ELSE OUTPUT aN ;
```

<br>

▶ 두 개 이상의 DATASET을 만드는데 DATA문에서 지정해 주고 어떠한 처리도 하지 않은 경우 :   

    

```{}
<SAS PROGRAM>

data ltour2 othrtou2; 
set arts; 
if guide='Lucas' then output ltour2; 
run ;
proc print data=ltour2; 
title "Data Set with Guide='Lucas'" ;
run ;
proc print data=othrtou2; 
title "Data Set with Other Guides" ;
run ;
```
 
 1) DATA문에서 생성할 새로운 DATASET ltour2 othrtou2 지정  
 2) IF guide='Lucas' THEN output ltour2;라고 하여 IF문은 ltour2 DATASET에 대해서만
   사용하고 othrtou2 DATASET에 아무런 처리 없음  
 3) 출력 결과  
    TITLE별로 보면 "Data Set with guide='Lucas'"인 경우 : 출력  
                   "Data Set with Other Guides"인 경우 : 출력 안 됨    
- DATA문에 지정만 해주고 아무런 작업을 하지 않았을 경우에 아무 내용도 포함되지 않음   


                        Data Set with guide='Lucas'                      

        Obs     city      nights    landcost    budget    guide

         1     Paris         8        1680       High     Lucas
         2     Newyork       6           .                Lucas


▶ TITLE별로 보면 "Data Set with guide='Lucas'"인 경우만 출력  
"Data Set with Other Guides"인 경우는 출력 안 됨  

```{}
proc print data=othrtou2; 
title "Data Set with Other Guides" ;
run ;
```

대신에

```{}
title "Data Set with Other Guides" ;
proc print data=othrtou2; 
run ;
```

- 위의 경우는 title만 출력됨.  

▶ 특정 조건에 맞는 관찰값들만 취하여 새로운 두 개의 DATASET을 만들 때 새로운 변수를 생성하여 추가시키는 경우 사용위치에 따라 결과가 달라진다.  

    

```{}
<SAS PROGRAM>

data lday othrday; 
set arts; 
if guide='Lucas' then output lday; 
else output othrday; 
days=nights+1 ;
run ;
proc print data=lday; 
title "Number of Days in Lucas's Tours" ;
run ;
proc print data=othrday; 
title "Number of Days in Other Guides' Tours" ;
run ;
```

    <OUTPUT>

                      Number of  Days in Lucas's Tours                  

    Obs     city      nights    landcost    budget    guide    days

     1     Paris         8        1680       High     Lucas      .
     2     Newyork       6           .                Lucas      .

<br>

                        Number of  Days in Other Guides' Tours               
         
    Obs    city         nights    landcost    budget     guide     days

     1     Rome            3         750      Medium    D'Amico      .
     2     London          6        1230      High      Wilson       .
     3     Madrid          3         370      Low       Torres       .
     4     Amsterdam       4         580      Low                    .


▶ OUTPUT문 뒤에 새로운 변수를 생성한 것으로 days변수의 값을 보면 night+1의 값이  아닌 결측값을 가지고 있다.  

<br>


```{}
<SAS PROGRAM>

data lday2 othrday2; 
set arts; 
days=nights+1 ;
if guide='Lucas' then output lday2; 
else output othrday2; 
run ;
proc print data=lday2; 
title "Number of Days in Lucas's Tours" ;
run ;
proc print data=othrday2; 
title "Number of Days in Other Guides' Tours" ;
run ;
```
 
▶ 두 개 이상의 DATASET을 OUTPUT문을 이용하여 만들고자 할 경우 :   
   새로운 변수를 생성하거나 관찰값을 변화시키는 문장은 output문장 전에 위치 할 것  
▶ OUTPUT문보다 뒤에 있는 경우 : 해당변수는 결측값만 가짐  

    <OUTPUT>

                      Number of  Days in Lucas's Tours                  

    Obs     city      nights    landcost    budget    guide    days

     1     Paris         8        1680       High     Lucas      9
     2     Newyork       6           .                Lucas      7

<br>

                       Number of  Days in Other Guides' Tours               

    Obs    city         nights    landcost    budget     guide     days

     1     Rome            3         750      Medium    D'Amico      4  
     2     London          6        1230      High      Wilson       7
     3     Madrid          3         370      Low       Torres       4
     4     Amsterdam       4         580      Low                    5


▶ OUTPUT문 앞에서 새로운 변수를 생성한 것으로 days변수의 값을 보면 night+1의 값을 가진다.  

<br>

▶ 조건문을 독립적으로 수행하여 새로운 DATASET만들기  

    

```{}
<SAS PROGRAM>

data ltour othrtour weektour daytour; 
set arts; 
if guide='Lucas' then output ltour; 
else output othrtour; 
if nights>=6 then output weektour; 
else output daytour; 
run ;
proc print data=ltour; 
title "Lucas's Tours" ;
run ;
proc print data=othrtour; 
title "Other Guides' Tours" ;
run ;
proc print data=weektour; 
title 'Tours Lasting a Week or More' ;
run ;
proc print data=daytour; 
title 'Tours Lasting Less Than a Week' ;
run ;
```

▶ 이미 생성된 DATASET을 이용하여 두 개 이상의 DATASET을 만드는데 두 개 이상의 
   조건을 부여하여 생성하는 것  
 - 두 개의 조건을 주어 각기 다른 DATASET을 만드는 것을 보여준다.  

▶ arts DATASET을 이용하여 각기 조건을 부여하여 ltour, othrtour, weektour, daytour
   DATASET 4개를 생성하려는 것  
 - 조건문  
   guide변수의 값이 ‘Lucas'인 경우 : ltour DATASET  
                     그 외의 경우 : othrtour DATASET  
   nights변수의 값이 6이상인 경우 : weektour DATASET  
                     그 외의 경우 : daytour DATASET  
    * 두 가지 조건은 독립적으로 수행된다.  

    <OUTPUT>

                           Lucas's Tours                            

        Obs     city      nights    landcost    budget    guide

         1     Paris         8        1680       High     Lucas
         2     Newyork       6           .                Lucas

<br>

                        Other Guides' Tours                         

      Obs    city         nights    landcost    budget     guide

       1     Rome            3         750      Medium    D'Amico
       2     London          6        1230      High      Wilson
       3     Madrid          3         370      Low       Torres
       4     Amsterdam       4         580      Low

<br>

                     Tours Lasting a Week OR More                    

        Obs     city      nights    landcost    budget    guide

         1     Paris         8        1680       High     Lucas
         2     London        6        1230       High     Wilson
         3     Newyork       6           .                Lucas

<br>

                   Tours Lasting Less Than a Week                   

      Obs    city         nights    landcost    budget     guide

       1     Rome            3         750      Medium    D'Amico
       2     Madrid          3         370      Low       Torres
       3     Amsterdam       4         580      Low


▶ 맨 처음 두 개의 결과는 guide 변수의 값이 ‘Lucas'인 경우와 그 외의 경우로 형성 나중 두 개는 nights변수의 값이 6이상인 경우와 그 외의 경우로 나뉘어서 형성되어 출력된다.  

